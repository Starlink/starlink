#!/bin/sh    
# The next line is executed by /bin/sh, but not Tcl \
exec $GAIA_DIR/gaia_tcl $0 ${1+"$@"}
#+
#   Name:
#      gaiacut

#   Purpose:
#      Apply intensity cuts to GAIA

#   Usage:
#      gaiacut clone_number lowcut|percentile [highcut]

#   Description:
#      Applies a given intensity cut to a displayed image in GAIA.
#      If two values are given then these are interpreted as simple
#      intensity limits, otherwise a single "percentile" cut is
#      applied.
#
#      Clones are specified by an integer numbers from 1 upwards.

#   Authors:
#      Peter W. Draper (PWD):

#  Copyright:
#     Copyright (C) 2002-2005 Central Laboratory of the Research Councils.
#     Copyright (C) 2006 Particle Physics & Astronomy Research Council.
#     All Rights Reserved.

#  Licence:
#     This program is free software; you can redistribute it and/or
#     modify it under the terms of the GNU General Public License as
#     published by the Free Software Foundation; either version 2 of the
#     License, or (at your option) any later version.
#
#     This program is distributed in the hope that it will be
#     useful, but WITHOUT ANY WARRANTY; without even the implied warranty
#     of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#     GNU General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with this program; if not, write to the Free Software
#     Foundation, Inc., 51 Franklin Street,Fifth Floor, Boston, MA
#     02110-1301, USA

#   History:
#      12-DEC-2002 (PWD):
#         Original version.
#-
#.

#  Check the command-line arguments.
if { $argc >= 2 } { 
   set clone [lindex $argv 0]
   set lowcut [lindex $argv 1]
   set percentile 1
   if { $argc >= 3 } {
      set percentile 0
      set highcut [lindex $argv 2]
   }
} else {
   puts stderr {Usage: gaiacut clone_number lowcut|percentile [highcut]}
   exit
}

#  Get the standard contact scripts.
source $env(GAIA_DIR)/connect_to_gaia.tcl

#  Open up connection to GAIA.
lassign [connect_to_gaia] gaia_fd created_instance

#  Command needs to performed by Skycat or derived object.
set cmd "skycat::SkyCat::get_skycat_images"
set images [send_to_gaia $gaia_fd remotetcl $cmd]

#  Got list so select the right clone. These start with number 1...
if { $clone == 0 } { 
   puts stderr "Clone numbers start from 1. So assuming 1"
   set clone 1
}

#  The clone <n> is called .gaia<n>.
set clonewidget ".gaia$clone"

#  See if this exists.
set cmd "winfo exists $clonewidget"
set exists [send_to_gaia $gaia_fd remotetcl $cmd]
if { ! $exists } {
   puts stderr "Failed to locate GAIA clone ($clone)"
   close $gaia_fd
   exit
}

#  Get the associated image name...
set cmd "$clonewidget get_image"
set image [send_to_gaia $gaia_fd remotetcl $cmd]
set cmd "$image get_image"
set image [send_to_gaia $gaia_fd remotetcl $cmd]

#  Construct the command needed to apply cut.
if { $percentile } { 
   set cmd "$image autocut -percent $lowcut"
} else {
   set cmd "$image cut $lowcut $highcut"
}
 
#  And send the command.
set ret [send_to_gaia $gaia_fd remotetcl $cmd]
if { $ret != "" } { 
   puts stderr "Failed to apply cut to image: ($ret)"
}
close $gaia_fd
exit
