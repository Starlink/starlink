#!/bin/sh
# The next line is executed by /bin/sh, but not Tcl \
exec $GAIA_DIR/gaia_tcl $0 ${1+"$@"}
#+
#   Name:
#      gaiadisp

#   Purpose:
#      Display images in a GAIA tool.

#   Usage:
#         gaiadisp [-p value] [-c component] image
#      or
#         gaiadisp [-p value] [-c component] image clone_number
#      or
#         gaiadisp [-p value] [-c component] image1 image2 image3 ...

#   Description:
#      This command displays images in GAIA. It has two basic modes
#      display a single image into a specified "clone", or display
#      a list of images into a series of automatically created clones.
#      Optional arguments allow for a percentile cut to be applied
#      and a data component of an NDF to be displayed (VARIANCE
#      or QUALITY).
#
#      Clones are specified by an integer number less than 1000.
#      The special number -1 indicates that a number should be generated.

#   Notes:

#   Authors:
#      Peter W. Draper (PWD):

#  Copyright:
#     Copyright (C) 1996-2005 Central Laboratory of the Research Councils.
#     Copyright (C) 2006 Particle Physics & Astronomy Research Council.
#     Copyright (C) 2007-2008 Science and Technology Facilities Council.
#     All Rights Reserved.

#  Licence:
#     This program is free software; you can redistribute it and/or
#     modify it under the terms of the GNU General Public License as
#     published by the Free Software Foundation; either version 2 of the
#     License, or (at your option) any later version.
#
#     This program is distributed in the hope that it will be
#     useful, but WITHOUT ANY WARRANTY; without even the implied warranty
#     of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#     GNU General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with this program; if not, write to the Free Software
#     Foundation, Inc., 59 Temple Place,Suite 330, Boston, MA
#     02111-1307, USA


#   History:
#      25-NOV-1996 (PWD):
#         Original version.
#      09-MAR-1998 (PWD):
#         Now uses the remote control interface, rather than send
#         mechanism (less X security complaints).
#      18-MAY-1999 (PWD):
#         Converted to use GAIA single binary gaia_stcl.
#      15-APR-2004 (PWD):
#         Merged with the gaiadispmany script that was never released.
#-
#.

#  Check the command-line arguments.

#  Handle the -p and -c arguments.
package require cmdline
set options {
   {p.arg  -1     "percentile cut for display (50-100)"}
   {c.arg  "DATA" "NDF component, DATA, VARIANCE or QUALITY"}
}

set usage ":
             gaiadisp \[options] image
             gaiadisp \[options] image clone_number
             gaiadisp \[options] image1 image2 image3 ...
options:"
array set params [::cmdline::getoptions argv $options $usage]

set percentile "$params(p)"
set component "$params(c)"
set clone -1

#  Allow shortname for component.
switch -glob $component {
   d* -
   D* {
      set component "DATA"
   }
   v* -
   V* {
      set component "VARIANCE"
   }
   q* -
   Q* {
      set component "QUALITY"
   }
   default {
      puts stderr "Unknown NDF component: \"$component\""
      puts stderr "Should be one of DATA, VARIANCE or QUALITY"
      exit
   }
}

#  Handle non option values (images and clone number).
set argc [llength $argv]
if { $argc == 1 } {
    if { [lindex $argv 0] == "" } {
       set argc 0
    }
}

if { $argc == 1 } {
   set clone ""
} elseif { $argc == 2 } {
   set clone [lindex $argv 1]
   if { [catch { expr $clone+1 }] } {
      set clone -1
   } else {
      set argv [lindex $argv 0]
   }
} elseif { $argc > 2 } {
   set clone -1
} else {
   set argv "-help"
   puts stderr [::cmdline::usage $options $usage]
   exit
}

#  Add GAIA_LIBRARY to autopath for some GAIA classes.
lappend auto_path $env(GAIA_DIR)

#  Useful to know when instance is created.
set created_instance 0

#  Open a socket to a GAIA application and return the file descriptor
#  for remote commands. If a GAIA isn't found then start one up.
proc connect_to_gaia {} {
   global env
   global created_instance

   #  Get the hostname and port info from the file ~/.rtd-remote,
   #  which is created by rtdimage when the remote subcommand is
   #  used.
   set tries 0
   while { 1 } {
      set needed 0

      #  Open the file containing the GAIA process information and read it.
      if {[catch {set fd [open $env(HOME)/.rtd-remote]} msg]} {
         set needed 1
      } else {
         lassign [read $fd] pid host port
         close $fd
      }

      #  See if the process is listening to this socket.
      if { ! $needed } {
         if {[catch {socket $host $port} msg]} {
            set needed 1
         } else {
            fconfigure $msg -buffering line
            return $msg
         }
      }

      #  If the process doesn't exist and we've not been around the
      #  loop already, then start a new GAIA.
      if { $needed && $tries == 0 } {
         puts stderr "Failed to connect to GAIA, starting new instance..."
         set created_instance 1
         exec $env(GAIA_DIR)/gaia.csh &
         #exec $env(GAIA_DIR)/tgaia &
      }

      #  Now either wait and try again or give up if waited too long.
      if { $needed && $tries < 500 } {
         #  Wait for a while and then try again.
         incr tries
         after 1000
      } elseif { $needed } {
         puts stderr "Sorry timed out: failed to display image in GAIA"
         exit 1
      }
   }
}

#  Send the command to GAIA and return the results or generate an error.
proc send_to_gaia {args} {
   global gaia_fd
   puts $gaia_fd "$args"
   lassign [gets $gaia_fd] status length
   set result {}
   if {$length > 0} {
      set result [read $gaia_fd $length]
   }
   if {$status != 0} {
      error "$result"
   }
   return "$result"
}

#  Open up connection to GAIA.
set gaia_fd [connect_to_gaia]

#  Command needs to performed by Skycat or derived object. We just
#  talk to the first window on the list.
set cmd "skycat::SkyCat::get_skycat_images"
set images [send_to_gaia remotetcl $cmd]

#  Got list so select first and ask about the parent (should be
#  top-level GAIA).
set ctrlwidget [lindex $images 0]
set cmd "winfo parent $ctrlwidget"
set gaia [send_to_gaia remotetcl $cmd]

#  Create a name parser.
set namer [::gaia::GaiaImageName .namer]

#  Loop over all images.
foreach image $argv {

   # Parse name.
   $namer configure -imagename $image
   if { ! [$namer exists] } {
      puts stderr "Cannot read image: $image"
      continue
   }

   #  Make it absolute (also stripping off tmp_mnt, if present).
   $namer absolute

   #  If it has a FITS extension then we need to protect the [].
   $namer protect

   #  Construct the command needed to display the image. Note
   #  complication when we create GAIA, need to display into the first
   #  clone.
   if { $created_instance || $clone == "" } {

      #  Apply component choice before open.
      set cmd "$gaia configure -component $component"
      send_to_gaia remotetcl $cmd

      set cmd "$gaia open [$namer fullname]"
      set created_instance 0
   } else {
      #  Component is passed as configuration arguments.
      set cmd "$gaia noblock_clone $clone [$namer fullname] 0 -component $component"
   }

   #  And send the command.
   set gaianame [send_to_gaia remotetcl $cmd]
   if { $gaianame == "" || [string match ".gaia*" $gaianame] } {
      puts stderr "Displayed image: $image"
   } else {
      puts stderr "Failed to display image: ($gaianame)"
   }

   #  If have a percentile cut apply that. Note this applies to the clone
   #  not the base gaia, unless this is the first image.
   if { $percentile >= 50.0 && $percentile <= 100.0 } {
      if { $gaianame == "" } {
         set cmd "$gaia get_image"
      } else {
         set cmd "$gaianame get_image"
      }
      set gaiaimage [send_to_gaia remotetcl $cmd]
      set cmd "$gaiaimage get_image"
      set image [send_to_gaia remotetcl $cmd]
      set cmd "$image autocut -percent $percentile"
      send_to_gaia remotetcl $cmd
   }
}
close $gaia_fd
exit
