      SUBROUTINE RTD1_GBND<T>( IMAGE, NX, NY, LBNDS, UBNDS, STATUS )
*+
*  Name:
*     RTD1_GBNDx

*  Purpose:
*     Finds a section that contains all the non-bad pixels in an image. 

*  Language:
*     Starlink Fortran-77

*  Invocation:
*     CALL RTD1_GBND<T>( IMAGE, NX, NY, LBNDS, UBNDS, STATUS )

*  Description:
*     This routine returns the bounds of the part of an image that
*     contains all the non-bad pixels.

*  Arguments:
*     IMAGE( NX, NY ) = <TYPE> (Given)
*        The image to be scanned for good pixels.
*     NX = INTEGER (Given)
*        The first dimension of IMAGE.
*     NY = INTEGER (Given)
*        The second dimension of IMAGE.
*     LBNDS( 2 ) = INTEGER (Returned)
*        The lower bounds of section of the image that contains
*        only good pixels. 
*     UBNDS( 2 ) = INTEGER (Returned)
*        The upper bounds of section of the image that contains
*        only good pixels. 
*     STATUS = INTEGER ({status_access_mode})
*        The global status.

*  Copyright:
*     Copyright (C) 1997 Central Laboratory of the Research Councils

*  Authors:
*     PDRAPER: Peter Draper (STARLINK - Durham University)
*     {enter_new_authors_here}

*  History:
*     2-DEC-1996 (PDRAPER):
*        Original version.
*     {enter_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE             ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'         ! Standard SAE contants
      INCLUDE 'PRM_PAR'         ! Primitive data constants
      
*  Arguments Given:
      INTEGER NX
      INTEGER NY
      <TYPE> IMAGE( NX, NY )
      
*  Arguments Returned:
      INTEGER LBNDS( 2 )
      INTEGER UBNDS( 2 )
      
*  Status:
      INTEGER STATUS            ! Global status

*  Local Variables:
      INTEGER I, J
      
*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Initializations.
      LBNDS( 1 ) = NX
      UBNDS( 1 ) = 1
      LBNDS( 2 ) = NY
      UBNDS( 2 ) = 1

*  Now loop over all image and find the bounds.
      DO 1 J = 1, NY
         DO 2 I = 1, NX
            IF ( IMAGE( I, J ) .NE. VAL__BAD<T> ) THEN 
               IF ( I .LT. LBNDS( 1 ) ) LBNDS( 1 ) = I
               IF ( I .GT. UBNDS( 1 ) ) UBNDS( 1 ) = I
               IF ( J .LT. LBNDS( 2 ) ) LBNDS( 2 ) = J
               IF ( J .GT. UBNDS( 2 ) ) UBNDS( 2 ) = J
            END IF
 2       CONTINUE
 1    CONTINUE

      END
* @(#)rtd1_gbndx.gen   1.1   96/12/03 16:49:01   96/12/03 16:49:16
