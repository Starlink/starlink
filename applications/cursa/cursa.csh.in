#!/bin/csh -f
#+
#  Name:
#     cursa.csh
#
#  Purpose:
#     Start the CURSA system from Unix shell.
#
#  Type of Module:
#     C shell script.
#
#  Invocation:
#     source cursa.csh
#
#  Description:
#     This procedure defining the links needed to execute
#     each application from the current directory.
#
#  Notes:
#     The installation target is set outside of this script.
#     A test is made to see if the environment variable INSTALL
#     has been set.
#
#  Authors:
#     BLY: M.J.Bly (Starlink, RAL)
#     ACD: Clive Davenhall (Starlink, ROE)
#     PWD: Peter W. Draper (JAC, Durham University)
#     {enter_new_authors_here}
#
#  History:
#     26-MAY-1995 (BLY):
#       Version for CURSA v1.0
#     14-OCT-1995 (ACD):
#       Added aliases for application catgscin (which is new in
#       release 1.1).
#     20-DEC-1996 (ACD):
#       Added aliases for application catselect (which is new in
#       release 2.1).
#     13-JUN-1997 (ACD):
#       Added aliases for applications catcoord, catchart, catchartrn
#       and catremote (which are new in release 3.1).  Also set
#       environment variable SKYCAT_CONFIG so that the default set
#       of remote catalogues is the copy of the HEASARC at Leicester.
#     13-NOV-1997 (ACD):
#       Added aliases for applications catphotomfit, catphotomtrn
#       and catphotomlst (which are new in release 4.1).
#     29-NOV-1998 (ACD):
#       Added aliases for application (Perl script) catremote.
#     {enter_changes_here}
#     29-NOV-1999 (ACD):
#       Added aliases for application catgrid.  Corrected the URL for
#       the default configuration file.
#     31-MAY-2001 (ACD):
#       Modified to correspond to the new catremote which is a Perl script
#       rather than a C++ program.
#     24-JAN-2006 (PWD):
#       Changed CATREM_CONFIG to point at a RAL machine. Still not ideal, but
#       works for the present.
#     01-OCT-2007 (PWD):
#       Use CURSA_DIR to define commands, not baked in paths. Use aliases
#       to start Perl scripts, not baked in interpreter (note that that
#       still happens, the scripts are just not ran up using it).
#-
#

#  Prepare to run ADAM applications if this has not been done already.
#  ===================================================================
#
#  Here look to see if there is an ADAM_USER directory.  If there is not
#  check whether or not there is an adam file that is not a directory.
#  If there is, issue a warning and exit.  Otherwise create the required
#  directory.
#
if ($?ADAM_USER) then
   set adamdir=$ADAM_USER
else
   set adamdir=$HOME/adam
endif

if (-d $adamdir) then
   echo -n
else
   if (-f $adamdir) then
      echo "You have a file called $adamdir"
      echo "Please rename this, or define the environment variable ADAM_USER"
      echo "to point to a directory for ADAM files."
      exit
   else
      mkdir $adamdir
   endif
endif
unset adamdir

#
#  Define aliases for the applications.
#  There should be a plain alias, and a package specific alias
#  so thatapplications that have conflicting command names are
#  still available.

alias catcopy         '$CURSA_DIR/catcopy'
alias cursa_catcopy   '$CURSA_DIR/catcopy'

alias catheader       '$CURSA_DIR/catheader'
alias cursa_catheader '$CURSA_DIR/catheader'

alias catpair         '$CURSA_DIR/catpair'
alias cursa_catpair   '$CURSA_DIR/catpair'

alias catsort         '$CURSA_DIR/catsort'
alias cursa_catsort   '$CURSA_DIR/catsort'

alias catview         '$CURSA_DIR/catview'
alias cursa_catview   '$CURSA_DIR/catview'

alias catgscin        '$CURSA_DIR/catgscin'
alias cursa_catgscin  '$CURSA_DIR/catgscin'

alias catselect       '$CURSA_DIR/catselect'
alias cursa_catselect '$CURSA_DIR/catselect'

alias catcoord        '$CURSA_DIR/catcoord'
alias cursa_catcoord  '$CURSA_DIR/catcoord'

alias catchart        '$CURSA_DIR/catchart'
alias cursa_catchart  '$CURSA_DIR/catchart'

alias catchartrn          '$CURSA_DIR/catchartrn'
alias cursa_catchartrn    '$CURSA_DIR/catchartrn'

alias catphotomfit        '$CURSA_DIR/catphotomfit'
alias cursa_catphotomfit  '$CURSA_DIR/catphotomfit'

alias catphotomtrn        '$CURSA_DIR/catphotomtrn'
alias cursa_catphotomtrn  '$CURSA_DIR/catphotomtrn'

alias catphotomlst        '$CURSA_DIR/catphotomlst'
alias cursa_catphotomlst  '$CURSA_DIR/catphotomlst'

alias catgrid         '$CURSA_DIR/catphotomlst'
alias cursa_catgrid   '$CURSA_DIR/catphotomlst'

#  Perl scripts, use the Starlink Perl.
if ( -e @STARPERL@ ) then
   set starperl=@STARPERL@
else
   if ( -e $STARLINK_DIR/bin/starperl ) then
      set starperl=$STARLINK_DIR/bin/starperl
   else
#  No Starlink Perl, see what the default one can achieve.
      set starperl=perl
   endif
endif

alias catcdsin        $starperl '$CURSA_DIR/catcdsin'
alias cursa_catcdsin  $starperl '$CURSA_DIR/catcdsin'

alias catremote       $starperl '$CURSA_DIR/catremote'
alias cursa_catremote $starperl '$CURSA_DIR/catremote'

# tcl/tk script.

alias xcatview        '$CURSA_DIR/xcatview'
alias cursa_xcatview  '$CURSA_DIR/xcatview'

#
#  Environment variables for catremote.
#  ===================================
#
#  Define the utility program to be used to submit the query.  The options
#  are:
#    "/star/bin/cursa/geturl" -- geturl utility,
#    "lynx -source"           -- lynx command line browser,
#    "java  UrlReader"        -- Java utility.
#    "wget -q -O -"           -- wget utility (probably RedHat Linux only).

setenv  CATREM_URLREADER '$CURSA_DIR/geturl'

#
#  Specify the configuration file to be used.  The URL given here is the
#  the CURSA default.

setenv CATREM_CONFIG "$CURSA_DIR/cursa.cfg"

#
#  Define the maximum number of objects which may be included in the
#  returned table.

setenv CATREM_MAXOBJ 200

#
#  Specify whether the URL constituting the query is echoed to the
#  command line.  The options re:
#    no   -  do not echo the URL (default),
#    yes  -  echo the URL.

setenv CATREM_ECHOURL no

#
#  Set the Java CLASSPATH environment variable to pick up the URLreader
#  (note that CLASSPATH has to be set rather than setting the corresponding
#  command line option because the latter does not work on alphas).

setenv CLASSPATH "${CURSA_DIR}:/usr/lib/netscape/java/classes"


#
#  Announce the availability of the CURSA commands.

echo ""
echo "   CURSA commands are now available -- (Version @PACKAGE_VERSION@)"
echo ""

#
# end
#
