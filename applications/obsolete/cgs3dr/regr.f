	SUBROUTINE REGR (X,Y,N,A,B,SIGA,SIGB,S)
C
C	UNWEIGHTED LINEAR LEAST SQUARES   KK  10-15-1985
C
C	THIS SUBROUTINE IS USED FOR LINEAR REGRESSION OF THE FORM
C
C	        Y = A + B*X
C
C	X	= INPUT ARRAY OF X-VALUES
C	Y 	= INPUT ARRAY OF Y-VALUES
C	N	= INPUT NUMBER OF DATA POINTS
C	A	= OUTPUT LEAST-SQUARES INTERCEPT
C	B	= OUTPUT LEAST-SQUARES SLOPE
C	SIGA	= UNCERTAINTY OF INTERCEPT
C	SIGB	= UNCERTAINTY OF SLOPE
C	S	= RMS RESIDUAL OF FIT
C
	VIRTUAL X(1), Y(1)
C
C	COMPUTE AVERAGE X AND Y
C
	XSUM = 0.0
	YSUM = 0.0
	DO 10 I = 1,N
	  XSUM = XSUM + X(I)
	  YSUM = YSUM + Y(I)
10	CONTINUE
	XAVE = XSUM/FLOAT(N)
	YAVE = YSUM/FLOAT(N)
C
C	COMPUTE SLOPE AND INTERCEPT
C
        IF ( N .LT. 2 ) THEN
          WRITE(*,'(1X,A)') 'REGR: # points must be >= 2'
          GOTO 900
        ENDIF
	SXX = 0.0
	SXY = 0.0
	SYY = 0.0
	DO 20 I = 1,N
	  Q = X(I) - XAVE
	  R = Y(I) - YAVE
	  SXX = SXX + Q*Q
	  SXY = SXY + Q*R
	  SYY = SYY + R*R
20	CONTINUE
	B = SXY/SXX
	A = YAVE - B*XAVE
C
C	COMPUTE SUM OF SQUARES OF RESIDUALS
C
	SSRES = 0.0
	DO 30 I = 1,N
	  YEST = A + B*X(I)
	  Z = Y(I) - YEST
	  SSRES = SSRES + Z*Z
30	CONTINUE
C
C	COMPUTE ERROR OF INTERCEPT AND SLOPE
C
        IF ( N .GT. 2 ) THEN
          S = SQRT(SSRES/(FLOAT(N) - 2.))
        ELSE
          S = 0.0
        ENDIF
	SIGA = S*SQRT(1./FLOAT(N) + XAVE*XAVE/SXX)
	SIGB = S/SQRT(SXX)
C
900     CONTINUE
	RETURN
	END
