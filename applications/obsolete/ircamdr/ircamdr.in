#!/bin/csh -f

#+
# file:        IRCAMDR_ROOT/ircamdr
# type:        C-shell script
# author:      Phil Daly
# description: Invokes the Unix-IRCAMDR command line software
# history:
#   29-may-95: original version (pnd@jach.hawaii.edu)
#   05-Jul-95: modified (caa@jach.hawaii.edu)
#   29-Nov-95: modified env. vars + added some aliases (caa@jach)
#   05-Dec-95: modified for ukirt operation (caa@jach.hawaii.edu)
#   12-AUG-04: Starlink CVS/autoconf (timj@jach)
# endhistory
# parameters: $1 is a directory /$HOME/
#             $2 is a UT-date   /UT=today/
#-

# If IRCAMDR_ROOT is not set, abort
if ( ${?IRCAMDR_ROOT} == 0 ) then
  setenv IRCAMDR_ROOT /ukirt_sw/ircamdr
endif

# Set some default values
echo ""
set defutdate  = "`date -u +%y%m%d`"
set defdatadir = "/ukirtdata/ircam_data/19${defutdate}/rodir"

# If not arguments supplied, get them all
if ( ${#argv} == 0 ) then
  # Set data directory
  set datadir = "${defdatadir}"
  set utdate = "${defutdate}"

else if ( ${#argv} == 1 ) then
  # Get data directory
  set datadir = "${argv[1]}"
  set utdate = "${defutdate}"

else if ( ${#argv} >= 2 ) then
  # Get data directory
  set datadir = "${argv[1]}"
  # Get the UT date
  set utdate = "${argv[2]}"
endif

# Check the UT date is of the form YYMMDD or YYYYMMDD
if ! ( "${utdate}" =~ [0-9][0-9][0-9][0-9][0-9][0-9] ) then
  if ! ( "${utdate}" =~ [0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9] ) then
    echo "${0}-f-invutdate, invalid UT date\!"
    exit 1
  endif
endif

# Set the IRCAM data directories
setenv ODIR  ../odir
setenv IDIR  ../idir
setenv RODIR ../rodir
setenv RIDIR ../ridir
setenv RGDIR ../rgdir

# Check the data directory is readable and writeable
if ( -r "${datadir}"  && -w "${datadir}" ) then
#  echo "cd ${datadir}"
  cd ${datadir}
else
  echo "-------------------------------------------------------"
  echo "WARNING : The data disk does not seem to be readable or"
  echo "          or writeable at the present time."
  echo "-------------------------------------------------------"
#  exit 1
endif

# Make sure ADAM_USER is defined
if ( ! -e ${HOME}/adam ) mkdir ${HOME}/adam
setenv ADAM_USER "`echo ${HOME}`/adam"

# Environment variables for ircam-clred operation
setenv LIRCAMDIR      ${IRCAMDR_ROOT}

# More specific env vars
if ( "`uname -m`" =~ [Ss][Uu][Nn]* ) then
  setenv SYSNODE SUN
else if ( "`uname -m`" =~ [Aa][Ll][Pp][Hh][Aa]* ) then
  setenv SYSNODE ALPHA
else
  setenv SYSNODE UNKNOWN
endif
setenv IRCAM_ENG ${HOME}
setenv TODAY "`date`"
setenv UTD ${defutdate}
setenv ROUTD "ro${defutdate}_"
setenv HDS_MAP 1

# Change path instead of aliasing commands
set path = ( $path ${IRCAMDR_ROOT} )
rehash

# Print out a welcome message
echo ""
echo "Welcome to IRCAM_CLRED/Portable-IRCAMDR V@PACKAGE_VERSION@ at UKIRT"

# Start icl and invoke ircamdr
icl ${IRCAMDR_ROOT}/god_ircamdr_ukirt.icl
echo ""
