#!/bin/csh
# Name:
#    make_unix_release
#
# Purpose:
#    Do unix specific bits required to make a release of polpack.
#
# Type of Module:
#    Shell script
#
# Description:
#    This command creates a compressed tar file containing a release of the 
#    polpack package. It is called from within the make_release script. 
#    
# Invocation:
#    make_unix_release
#
# Authors:
#    DSB: David S. Berry (STARLINK)
#    {enter_new_authors_here}
#
# History:
#    18-JUL-1997 (DSB):
#       Original version, based on PDA equivalent.
#    {enter_changes_here}
#
# Bugs:
#    {note_any_bugs_here}
#-

#  Ensure the sdt grp command is available.
      source $SDT_DIR/startup.csh

#  Ensure the sst prohlp command is available.
      source /dsbwork/bin/sst/start

#  Store the name of the release directory, and go there.
      set REL_DIR = ${TEMPDIR}/polpack_${SYSTEM}
      cd ${REL_DIR}

#  See if the hypertext documents are to be re-created.
#      echo -n "Re-create polka.htx? (y/n): "
#      set ans = $<
#      if( $ans == "y" ) then
#         star2html polka.tex
#         tar -cvhf ./polka.htx_tar polka.htx/*
#         rm -rf polka.htx
#         mv ./polka.htx_tar ${POLPACK_DEV}/..
#      endif

#  Copy the hypertext docs to the release directory.
      cp ${POLPACK_DEV}/../polka.htx_tar ${REL_DIR}
      cp ${POLPACK_DEV}/../sun223.htx_tar ${REL_DIR}

#  Make the ndg source tar file.
      ${NDG_SYS}/dev/bin/make_source.csh

#  Ensure all files are accessable.
      chmod 777 *

#  Create the on-line help file.
      foreach file (`grp user_tasks`)
         prohlp in=$file out=$file.help
      end
      cat polpack.hlp *.help > new.hlp
      mv new.hlp polpack.hlp
      rm *.help

# Create the monolith interface file.
      rm -f polpack_mon.ifl
      echo "Monolith polpack_mon" > ttt
      foreach file (*.ifl)
         cat $file >> ttt
      end
      echo "endmonolith" >> ttt
      mv -f ttt polpack_mon.ifl

#  Edit in the current package version number into any required source files.
      set vers = `grep "PKG_VERS = " makefile`
      if ( "$vers" == "" ) then
         echo "Can't get package version number from makefile."
         exit
      endif

      foreach file (pol1_ptvrs.f pol1_ptvrc.f polpack_mon.f)
         sed -e "s#PKG_VERS#$vers[3]#g" $file > temp
         mv -f temp $file
      end

#  Tar up the source files and then remove them.
      tar -cvh -f ${TEMPDIR}/polpack_source.tar `grp unix_source` 
      rm -f `grp unix_source` 
      mv ${TEMPDIR}/polpack_source.tar ${REL_DIR}

#  Make a sub-directory to hold the eps files containing figures for 
#  sun223.tex.
      mkdir ${REL_DIR}/sun223_figures
      mv `grp figs` ${REL_DIR}/sun223_figures

#  Create the tar file holding the IRAF files.
#      source ${POLPACK_DEV}/.sdtrc
#      make_iraf

#  Make the ndg and ctg source tar files.
      ${NDG_SYS}/dev/bin/make_source.csh
      ${CTG_SYS}/dev/bin/make_source.csh

#  Tar up all the release files (source and non-source), and then remove
#  them.
      tar -cvh -f ${TEMPDIR}/polpack.tar `grp unix_total` 
      rm -rf `grp unix_total` 
      mv ${TEMPDIR}/polpack.tar ${REL_DIR}

#  Compress it.
      compress polpack.tar

      exit
