      SUBROUTINE KPS1_LFTQ<T>( ORDER, AXIS, NRANGE, RANGES, DIMS,
     :                         DATS, AS, BS, WRK1, WRK2, STATUS )
*+
*  Name:
*     KPG1_LFTx

*  Purpose:
*     Fits polynomials to all lines of data that lie parallel to
*     an axis.  Quickest version, no weights or bad pixels.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL KPS1_LFTQx( ORDER, AXIS, NRANGE, RANGES, DIMS,
*                      DATS, AS, BS WRK1, WRK2, STATUS )

*  Description:
*     This routine fits polynomials, of a given order, to all lines of
*     data that lie parallel to a given axis, and lie within ranges
*     along that axis.  The fits are not weighted and the data must not
*     contain any bad pixels.  When these conditions are true lines can
*     be fitted with less workspace requirements and speed ups because
*     the A matrix is the same for all lines (and AS is just size
*     (ORDER+1,ORDER+1)).
*
*     The polynomial coefficients are returned in the BS array.

*  Arguments:
*     ORDER = INTEGER (Given)
*        The order of polynomial to fit.  Starts at 0 for a constant.
*     AXIS = INTEGER (Given)
*        The axis defining the lines to be fit.
*     NRANGE = INTEGER (Given)
*        The number of values in RANGES, must be an even number.
*     RANGES( NRANGE ) = INTEGER*8 (Given)
*        Pairs of array co-ordinates along the axis.  Only data within
*        these ranges will be used in the fit.
*     DIMS( NDF__MXDIM ) = INTEGER*8 (Given)
*        The dimensions of the input data.  Extra dimension should have
*        size 1 up to NDF__MXDIM.
*     DATS( * ) = ? (Given and Returned)
*        The data with dimensions DIMS.  The fit will be subtracted from
*        this if requested.
*     AS( ORDER + 1, ORDER + 1 ) = DOUBLE PRECISION (Returned)
*        Workspace for least-squares A matrix.
*     BS( ORDER + 1, * ) = DOUBLE PRECISION (Returned)
*        Workspace for least-squares B vectors cumulative sums.
*        On exit this contains the coefficients of the polynomials.
*
*        Since there are ORDER + 1 of these for each line, the size of
*        the last dimension of this should be the product of all the
*        dimensions that are not the fit axis.  The coefficients will be
*        accessed in Fortran order for the axis.  For instance if the
*        third axis is fit in a cube then this array has an effective
*        shape of (ORDER+1,NX,NY), if the second axis is chosen then the
*        shape is (ORDER+1,NX,NZ) etc.
*     WRK1( ORDER + 1 ) = DOUBLE PRECISION (Returned)
*        Workspace.
*     WRK2( ORDER + 1 ) = INTEGER (Returned)
*        Workspace.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  This routine works in n-D, where n is 1 to 7.  Even if the
*     array has actually fewer dimensions there is negligible loss of
*     efficiency to supply dummy (=1) higher dimensions.
*
*     - To work as quickly as possible the fits are performed by running
*     over the data in fast order and accumulating the least-squares
*     matrices for each line as and when the data are encountered.  The
*     solutions are then determined in a single separate section.  In
*     this version the A sums are the same for all lines (since they
*     depend only on the ordinates and they have the same values for
*     every line).
*
*     - There is a routine for each numeric data type: replace "x" in
*     the routine name by D, R, I, W, UW, B or UB as appropriate.  The
*     base and arrays supplied to the routine must have the data type
*     specified.

*  Copyright:
*     Copyright (C) 2005-2006 Particle Physics & Astronomy Research
*     Council.
*     Copyright (C) 2008 Science and technology Facilities Council.
*     All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     PWD: Peter W. Draper (JAC, Durham University)
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     20-SEP-2005 (PWD):
*        Original version based upon KPS1_LFTx.
*     18-JAN-2006 (PWD):
*        Re-write counters into main loop so that the limits
*        are used correctly.
*     2006 April 12 (MJC):
*        Remove unused variable and wrapped long lines.
*     2008 May 15 (MJC):
*        Interchange NRANGE and RANGES, and DIMS and DATS arguments
*        to standard order.
*     2008 May 19 (MJC):
*        Do not test for bad values as documented in the description.
*     {enter_further_changes_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'NDF_PAR'          ! NDF public constants

*  Arguments Given:
      INTEGER ORDER
      INTEGER AXIS
      INTEGER NRANGE
      INTEGER*8 RANGES( NRANGE )
      INTEGER*8 DIMS( NDF__MXDIM )

*  Arguments Given and Returned:
      <TYPE> DATS( * )
      DOUBLE PRECISION AS( ORDER + 1, ORDER + 1 )
      DOUBLE PRECISION BS( ORDER + 1, * )
      DOUBLE PRECISION WRK1( * )
      INTEGER WRK2( * )

*  Status:
      INTEGER STATUS                ! Global status

*  Local Variables:
      INTEGER*8 B                   ! Pixel index of work arrays
      INTEGER*8 DIMSB( NDF__MXDIM ) ! Dimensions excluding the axis
      DOUBLE PRECISION DVAL         ! The current data value
      INTEGER*8 I( 7 )              ! Equivalences of I1-I7 loop variables
      INTEGER*8 I1, I2, I3, I4, I5, I6, I7 ! Loop variables
      INTEGER*8 IP1, IP2, IP3, IP4, IP5, IP6, IP7 ! Loop variables
      INTEGER*8 J                   ! Loop counter
      INTEGER*8 K                   ! Loop counter
      INTEGER*8 L                   ! Loop counter
      INTEGER*8 LB                  ! Bounds loop counter
      INTEGER*8 LLBND( NDF__MXDIM ) ! Loop lower bounds
      INTEGER*8 LUBND( NDF__MXDIM ) ! Loop upper bounds
      INTEGER*8 STRID( NDF__MXDIM ) ! Dimension strides
      INTEGER*8 STRIDA( NDF__MXDIM )! Dimension strides excluding axis
      INTEGER*8 STRIDB( NDF__MXDIM )! Dimension strides excluding axis
      <TYPE> TVAL                   ! The current data value
      INTEGER*8 WEL                 ! Workspace elements
      DOUBLE PRECISION X            ! The pixel co-ordinate

*  Equivalences so we can index loop variables by AXIS.
      EQUIVALENCE( I1, I( 1 ) )
      EQUIVALENCE( I2, I( 2 ) )
      EQUIVALENCE( I3, I( 3 ) )
      EQUIVALENCE( I4, I( 4 ) )
      EQUIVALENCE( I5, I( 5 ) )
      EQUIVALENCE( I6, I( 6 ) )
      EQUIVALENCE( I7, I( 7 ) )

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! Declare NUM_ conversion functions
      INCLUDE 'NUM_DEC_<T>'      ! Declare NUM_ arithmetic functions
      INCLUDE 'NUM_DEF_CVT'      ! Define NUM_ conversion functions
      INCLUDE 'NUM_DEF_<T>'      ! Define NUM_ arithmetic functions

*.

*  Check the inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Calculate the strides for each dimension.
      STRID( 1 ) = 1
      DO  J = 2, NDF__MXDIM
         STRID( J ) = STRID( J - 1 ) * DIMS( J - 1 )
      END DO

*  Calculate the strides for each dimension, this time excluding AXIS
*  so we can step around the workspace final dimension.  WEL is the
*  total number of workspace elements or the number of trends to fit.
*  First copy all dimensions, except that for AXIS.
      K = 1
      WEL = 1
      DO J = 1, NDF__MXDIM
         IF ( J .NE. AXIS ) THEN
            DIMSB( K ) = DIMS( J )
            WEL = WEL * DIMS( J )
            K = K + 1
         END IF
      END DO
      DIMSB( NDF__MXDIM ) = 0

*  Create strides for this new dimensionality and count workspace
*  elements.
      STRIDB( 1 ) = 1
      DO J = 2, NDF__MXDIM - 1
         STRIDB( J ) = STRIDB( J - 1 ) * DIMSB( J - 1 )
      END DO

*  Create strides that match those of the full array.
      K = 1
      DO J = 1, NDF__MXDIM
         IF ( J .NE. AXIS ) THEN
            STRIDA( J ) = STRIDB( K )
            K = K + 1
         ELSE
            STRIDA( J ) = 0
         END IF
      END DO

*  Initialise the workspaces used for the cumulative sums.
      DO J = 1, WEL
         DO L = 1, ORDER + 1
            BS( L, J ) = 0.0D0
         END DO
      END DO
      DO J = 1, ORDER + 1
         DO L = 1, ORDER + 1
            AS( L, J ) = 0.0D0
         END DO
      END DO

*  Set the loop bounds.  These are the dimensions of all axes, except
*  for the one that defines the ranges.
      DO J = 1, NDF__MXDIM
         LLBND( J ) = 1
         LUBND( J ) = DIMS( J )
      END DO

*  Determine the A matrix sums.  These are the same for all lines so can
*  be done just once.
      DO LB = 1, NRANGE, 2
         DO L = RANGES( LB ), RANGES( LB + 1 )
            X = DBLE( L )
            DO K = 1, ORDER + 1
               DO J = K, ORDER + 1
                  IF ( ( K + J - 2 ) .NE. 0 ) THEN
                     AS( K, J ) = AS( K, J ) + ( X ** ( K + J - 2 ) )
                  ELSE
                     AS( K, J ) = AS( K, J ) + 1
                  END IF
                  AS( J, K ) = AS( K, J )
               END DO
            END DO
         END DO
      END DO

*  Now loop over all input pixels within the ranges.  Note we use a
*  fully expanded set of dimensions as we need to run over the values
*  in fast order and maintain an additional stepping over the workspace
*  sums. (The workspace stepping is done using B and the data array
*  IP7-1).

*  Outer loop is over the bounds along the selected axis.
      DO LB = 1, NRANGE, 2
         LLBND( AXIS ) = RANGES( LB )
         LUBND( AXIS ) = RANGES( LB + 1 )

*  For each possible axis.  Note these outer loops are usually done
*  NRANGE/2 times only.
         B = 1
         DO I7 = LLBND( 7 ), LUBND( 7 )
            IP7 = ( I7 - 1 ) * STRID( 7 )
            DO I6 = LLBND( 6 ), LUBND( 6 )
               IP6 = IP7 + ( I6 - 1 ) * STRID( 6 )
               DO I5 = LLBND( 5 ), LUBND( 5 )
                  IP5 = IP6 + ( I5 - 1 ) * STRID( 5 )
                  DO I4 = LLBND( 4 ), LUBND( 4 )
                     IP4 = IP5 + ( I4 - 1 ) * STRID( 4 )
                     DO I3 = LLBND( 3 ), LUBND( 3 )
                        IP3 = IP4 + ( I3 - 1 ) * STRID( 3 )
                        DO I2 = LLBND( 2 ), LUBND( 2 )
                           IP2 = IP3 + ( I2 - 1 ) * STRID( 2 )
                           DO I1 = LLBND( 1 ), LUBND( 1 )
                              IP1 = IP2 + I1

*  Increment sums for this pixel.
                              TVAL = DATS( IP1 )
                              DVAL = NUM_<T>TOD( TVAL )
                              CALL KPS1_LFTPS( DBLE( I( AXIS ) ),
     :                                         DVAL, 1.0D0, ORDER,
     :                                         .TRUE., AS, BS( 1, B ) )

*  Move to the next workspace pixel.
                              B = B + STRIDA( 1 )
                           END DO
                           B = B + STRIDA( 2 ) - STRIDA( 1 ) * DIMS( 1 )
                        END DO
                        B = B + STRIDA( 3 ) - STRIDA( 2 ) * DIMS( 2 )
                     END DO
                     B = B + STRIDA( 4 ) - STRIDA( 3 ) * DIMS( 3 )
                  END DO
                  B = B + STRIDA( 5 ) - STRIDA( 4 ) * DIMS( 4 )
               END DO
               B = B + STRIDA( 6 ) - STRIDA( 5 ) * DIMS( 5 )
            END DO
            B = B + STRIDA( 7 ) - STRIDA( 6 ) * DIMS( 6 )
         END DO
      END DO

*  Solve linear equations and get the polynomial coefficients.  Note
*  that A is just inverted once this time.  The inversion also relies
*  on WRK2 being unmodified.
      CALL KPS1_LFTSV( ORDER, AS, .TRUE., BS( 1, 1 ), WRK1, WRK2,
     :                 STATUS )
      DO J = 2, WEL
         CALL KPS1_LFTSV( ORDER, AS, .FALSE., BS( 1, J ), WRK1, WRK2,
     :                    STATUS )
      END DO

      END
