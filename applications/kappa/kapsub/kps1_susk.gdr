      SUBROUTINE KPS1_SUSK<T>( NX, X, NXKNOT, XKNOT, STATUS )

*+
*  Name:
*     KPS1_SUSKx

*  Purpose:
*     To place interior knots so as to maintain an even distribution
*     of data points between the knot positions when fitting spline
*     functions to data.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL KPS1_SUSKx( NX, X, NXKNOT, XKNOT, STATUS )

*  Arguments:
*     NX = INTEGER*8 (Given)
*        The number of data points.
*     X( NX ) = ? (Given)
*        The co-ordinates of the points.
*     NXKNOT = INTEGER (Given)
*        The number of interior knots to be defined.
*     XKNOT( NXKNOT ) = ? (Returned)
*        The x positions of the interior knots defined.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  Uses the magic-value method for bad or undefined pixels.
*     -  There is a routine for double precision or real data types:
*     replace "x" in the routine name by D or R as appropriate.  The
*     X and XKNOT arguments supplied to the routine must have the data
*     type specified.

*  Copyright:
*     Copyright (C) 1990 Science & Engineering Research Council.
*     Copyright (C) 1996 Central Laboratory of the Research Councils.
*     All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     1990 Jan 31 (MJC):
*        Original version based on the EDRS code of the same name.
*     1996 October 15 (MJC):
*        Renamed from SETKNT.  Made generic.  Transposed X and NX
*        arguments.  Modern style.
*     20-FEB-2020 (DSB):
*        Support huge arrays.
*     {enter_further_changes_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants

*  Arguments Given:
      INTEGER*8 NX               ! Number of data points
      <TYPE> X( NX )             ! Co-ordinates of the data
      INTEGER NXKNOT             ! Number of interior knots

*  Arguments Returned:
      <TYPE> XKNOT( NXKNOT )     ! Positions of the interior knots

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      REAL DATAPT                ! Number of data points just less than
                                 ! the required knot value
      REAL FRAC                  ! Interpolation fraction
      INTEGER I                  ! Loop counter
      INTEGER*8 NDAT1            ! Index to the lower data point used in
                                 ! the interpolation
      INTEGER*8 NDAT2            ! Index to the upper data point used in
                                 ! the interpolation
      REAL XINTVL                ! Number of data points per knot
                                 ! interval

*.

*  Check the inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Find the mean number of data points per knot interval.
      XINTVL = REAL( NX - 1 ) / ( REAL( NXKNOT ) + 1.0 )

*  Count through each knot, calculating the number of data points less
*  than the required knot value.
      DO  I = 1, NXKNOT
         DATAPT = 1.0 + XINTVL * REAL( I )

*  The result is not usually an integer, so interpolate linearly
*  between the ordered data positions.
         NDAT1 = INT( DATAPT, KIND=8 )
         NDAT2 = MIN( NX, NDAT1 + 1 )
         FRAC = DATAPT - REAL( NDAT1 )
         XKNOT( I ) = X( NDAT1 ) * ( 1.0 - FRAC ) + X( NDAT2 ) * FRAC
      END DO

      END
