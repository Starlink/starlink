#!/bin/tcsh
# Name:
#    make_unix_release
#
# Purpose:
#    Do unix specific bits required to make a release of atools.
#
# Type of Module:
#    Shell script
#
# Description:
#    This command creates a compressed tar file containing a release of the 
#    atools package. It is called from within the make_release script. 
#    
# Invocation:
#    make_unix_release
#
# Authors:
#    DSB: David S. Berry (STARLINK)
#    {enter_new_authors_here}
#
# History:
#    17-JAN-2001 (DSB):
#       Original version, based on KAPPA equivalent.
#    {enter_changes_here}
#
# Bugs:
#    {note_any_bugs_here}
#-

#  Ensure the sdt grp command is available.
      source $SDT_DIR/startup.csh

#  Ensure the sst prohlp command is available.
      source /dsbwork/bin/sst/start

#  Store the name of the release directory, and go there.
      set REL_DIR = ${TEMPDIR}/atools_${SYSTEM}
      cd ${REL_DIR}
      echo "Moved to $PWD"

#  Copy the hypertext docs to the release directory.
#      echo "Copying the hypertext docs to the release directory."
#      cp ${ATOOLS_DEV}/../sun95.htx_tar ${REL_DIR}
#      cp ${ATOOLS_DEV}/../sun221.htx_tar ${REL_DIR}

#  Ensure all files are accessable.
      echo "Setting protection for all files"
      chmod 777 *

#  Edit in the current package version number into any required source files.
      echo "Editing the current package version number into source files"
      set vers = `grep "PKG_VERS = " makefile`
      if ( "$vers" == "" ) then
         echo "Can't get package version number from makefile."
         exit
      endif

      foreach file (atools.ifd)
         sed -e "s#PKG_VERS#$vers[3]#g" $file > temp
         mv -f temp $file
      end

#  Create the on-line help file.
      foreach file (`grp user_tasks`)
         prohlp in=$file out=$file.help
      end
      cat atools.star-hlp *.help > new.hlp
      mv new.hlp atools.star-hlp
      rm *.help

#  Generate the ifl files, and the icl and csh start-up scripts. 
      echo "Generating ifl files, etc form the ifd file"
      ifd2star atools

#  Generate the IRAF files.
      echo "Generating iraf files"
      ifd2iraf atools

#  Generate the IRAF documentation.
      mv atools.star-hlp atools.hlp
      ifd_irafhlpgen atools

#  Copy files to iraf directory.
      set airaf = "$HOME/iraf/atools"
      /bin/rm -rf $airaf
      mkdir $airaf
      cp root.hd $airaf
      cp _atools.hd $airaf
      cp atools.hd $airaf
      cd $airaf/..

#  Create a CL script which will create the help database.
      cat > temp.cl << FOO
set atools = /home/dsb/iraf/atools/
so
mkhelpdb $airaf/root.hd helpdb.mip
lo
FOO

# Execute the cl script, and copy the helpdb.mip file back.
      cl < temp.cl
      cp helpdb.mip $REL_DIR
      rm temp.cl
      cd $REL_DIR

#  Create each of the tar files. First do the system-independant ones
#  listed in group TAR_FILES_A...
      foreach file (`grp tar_files_a`)
         echo "Creating ${file}.tar"
         tar -cvh -f ${TEMPDIR}/${file}.tar `grp ${file}`
         rm -f `grp ${file}`
         mv ${TEMPDIR}/${file}.tar ${REL_DIR}
      end

#  Tar up all the release files and then remove them.
      echo "Creating atools.tar"
      chmod 644 `grp unix_total`
      chmod 755 `find . -type d`
      chmod 755 mk
      tar -cvh -f ${TEMPDIR}/atools.tar `grp unix_total` 
      rm `grp unix_total` 
      mv ${TEMPDIR}/atools.tar ${REL_DIR}
      rm -rf doc atools_mon.ifd

#  Compress it.
      echo "Compressing atools.tar"
      compress atools.tar

# Remove any spare files.
      mv atools.tar.Z ../
      rm -f *
      mv ../atools.tar.Z .

      exit
