      SUBROUTINE KPG1_AXEX<T>( EL, CENTRE, USEERR, ERROR, USEWID, WIDTH,
     :                         NSIGMA, ASTART, AEND, STATUS )
*+
*  Name:
*     KPG1_AXEXx

*  Purpose:
*     Calculates the extent of an NDF along an axis.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL KPG1_AXEXx( EL, CENTRE, USEERR, ERROR, USEWID, WIDTH,
*                      NSIGMA, ASTART, AEND, STATUS )

*  Description:
*     This routine calculates the starting and ending positions of an
*     NDF's pixels along an axis, optionally taking account of the
*     axis width and error values.

*  Arguments:
*     EL = INTEGER (Given)
*        The number of elements in the axis arrays.
*     CENTRE( EL ) = ? (Given)
*        The centres of the pixels on the axis.
*     USEERR = LOGICAL (Given)
*        Use the error array and NSIGMA in the calculation of the
*        extent of the axis.
*     ERROR( EL ) = ? (Given)
*        The errors of the pixel centres on the axis.  It is only
*        required (accessed) when USEERR = .TRUE..
*     USEWID = LOGICAL (Given)
*        Use the width array in the calculation of the extent of the
*        axis.
*     WIDTH( EL ) = ? (Given)
*        The widths of the pixels on the axis.
*     NSIGMA = ? (Given)
*        Number of multiples of the error to use in the calculation
*     ASTART = ? (Returned)
*        If the axis centre positions increase with NDF pixel index,
*        this argument returns the axis position of the edge of the
*        first pixel which has the lower co-ordinate. Otherwise it
*        returns the axis position of the edge with the higher
*        co-ordinate.
*     AEND = ? (Returned)
*        If the axis centre positions increase with NDF pixel index,
*        this argument returns the axis position of the edge of the
*        last pixel which has the higher co-ordinate. Otherwise it
*        returns the axis position of the edge with the lower
*        co-ordinate.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Copyright:
*     Copyright (C) 1996 Central Laboratory of the Research Councils.
*     All Rights Reserved.

*  Licence:
*     This programme is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*     
*     This programme is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE.  See the GNU General Public License for more details.
*     
*     You should have received a copy of the GNU General Public License
*     along with this programme; if not, write to the Free Software
*     Foundation, Inc., 51, Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     1996 October 2 (MJC):
*        Original version.
*     {enter_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-
      
*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! VAL__ constants

*  Arguments Given:
      INTEGER EL
      <TYPE> CENTRE( EL )
      LOGICAL USEERR
      <TYPE> ERROR( EL )
      LOGICAL USEWID
      <TYPE> WIDTH( EL )
      <TYPE> NSIGMA

*  Arguments Returned:
      <TYPE> ASTART
      <TYPE> AEND

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      <LTYPE> MAXE               ! Maximum axis centre plus error
      INTEGER MAXPOS             ! Index of maximum centre plus error
      <LTYPE> MINE               ! Minimum axis centre plus error
      INTEGER MINPOS             ! Index of minimum centre plus error
      INTEGER NINVAL             ! Number of bad centres and/or errors
      <LTYPE> THRESH( 2 )        ! Thresholds for finding the range

*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  The widths must not overlap but the extreme axis centre plus or
*  minus an error (especially if NSIGMA is greater than one) can.
*  So work through the whole axis centre and error arrays to find the
*  maximum and minimum.
      IF ( USEERR ) THEN
         THRESH( 1 ) = VAL__MIN<T>
         THRESH( 2 ) = VAL__MAX<T>
         CALL KPG1_MXME<T>( .TRUE., EL, CENTRE, ERROR, NSIGMA, THRESH,
     :                      NINVAL, MAXE, MINE, MAXPOS, MINPOS, STATUS )
      END IF

*  If the axis centre values increase with pixel index, then calculate
*  the extent.
      IF ( CENTRE( 1 ) .LE. CENTRE( EL ) ) THEN

*  Include both the width and the errors in the calculation.
         IF ( USEERR .AND. USEWID ) THEN
            ASTART = MIN( CENTRE( 1 ) - ABS( WIDTH( 1 ) ) / 2<CONST>,
     :               MINE )
            AEND = MAX( CENTRE( EL ) + ABS( WIDTH( EL ) ) / 2<CONST>,
     :             MAXE )

*  Allow for the errors.
         ELSE IF ( USEERR ) THEN
            ASTART = MINE
            AEND = MAXE

*  Allow for the widths.
         ELSE IF ( USEWID ) THEN
            ASTART = CENTRE( 1 ) - ABS( WIDTH ( 1 ) ) / 2<CONST>
            AEND = CENTRE( EL ) +  ABS( WIDTH ( EL ) ) / 2<CONST>

*  Just return the limiting axis centres.
         ELSE
            ASTART = CENTRE( 1 )
            AEND = CENTRE( EL )

         END IF
 
*  Otherwise calculate the extent appropriate to decreasing axis centre
*  values.
      ELSE
         IF ( USEERR .AND. USEWID ) THEN
            ASTART = MAX( CENTRE( 1 ) + ABS( WIDTH( 1 ) ) / 2<CONST>,
     :               MAXE )
            AEND = MAX( CENTRE( EL ) - ABS( WIDTH( EL ) ) / 2<CONST>,
     :             MINE )

*  Allow for the errors.
         ELSE IF ( USEERR ) THEN
            ASTART = MAXE
            AEND = MINE

*  Allow for the widths.
         ELSE IF ( USEWID ) THEN
            ASTART = CENTRE( 1 ) + ABS( WIDTH ( 1 ) ) / 2<CONST>
            AEND = CENTRE( EL ) - ABS( WIDTH ( EL ) ) / 2<CONST>

*  Just return the limiting axis centres.
         ELSE
            ASTART = CENTRE( 1 )
            AEND = CENTRE( EL )

         END IF
      END IF

      END
