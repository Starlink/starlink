      SUBROUTINE KPG1_MUL<T>( BAD, VAR, EL, A, VA, B, VB, C, VC, NERR,
     :                        STATUS )
*+
*  Name:
*     KPG1_MULx
 
*  Purpose:
*     Multiplies two vectorised arrays with optional variance
*     information.
 
*  Language:
*     Starlink Fortran 77
 
*  Invocation:
*     CALL KPG1_MULx( BAD, VAR, EL, A, VA, B, VB, C, VC, NERR, STATUS )
 
*  Description:
*     The routine forms the product of two vectorised arrays, with
*     optional variance information. Bad value checking is also
*     performed if required.
 
*  Arguments:
*     BAD = LOGICAL (Given)
*        Whether it is necessary to check for bad values in the input
*        arrays.  The routine will execute more rapidly if this checking
*        can be omitted.
*     VAR = LOGICAL (Given)
*        Whether associated variance information is to be processed.
*     EL = INTEGER (Given)
*        Number of array elements to process.
*     A( EL ) = ? (Given)
*        First array of data to be multiplied.
*     VA( EL ) = ? (Given)
*        Variance values associated with the array A.  Not used if VAR
*        is set to .FALSE..
*     B( EL ) = ? (Given)
*        Second array of data to be multipled.
*     VB( EL ) = ? (Given)
*        Variance values associated with the array B.  Not used if VAR
*        is set to .FALSE..
*     C( EL ) = ? (Returned)
*        Result of multiplying arrays A and B.
*     VC( EL ) = ? (Returned)
*        Variance values associated with the array C.  Not used if VAR
*        is set to .FALSE..
*     NERR = INTEGER (Returned)
*        Number of numerical errors that occurred during the
*        calculations.
*     STATUS = INTEGER (Given and Returned)
*        The global status.
 
*  Notes:
*     -  There is a routine for each of the data types integer, real
*     and double precision: replace "x" in the routine name by I, R or
*     D as appropriate.  The arrays passed to this routine should all
*     have the specified data type.
*     -  This routine will handle numerical overflow.  If overflow
*     occurs, then affected output array elements will be set to the
*     "bad" value. A count of the numerical errors which occur is
*     returned via the NERR argument.
 
*  Copyright:
*     Copyright (C) 1990, 1991 Science & Engineering Research Council.
*     Copyright (C) 1996, 2004 Central Laboratory of the Research
*                   Councils.
*     All Rights Reserved.

*  Licence:
*     This programme is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*     
*     This programme is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE.  See the GNU General Public License for more details.
*     
*     You should have received a copy of the GNU General Public License
*     along with this programme; if not, write to the Free Software
*     Foundation, Inc., 59, Temple Place, Suite 330, Boston, MA
*     02111-1307, USA.

*  Authors:
*     RFWS: R.F. Warren-Smith (STARLINK)
*     MJC: Malcolm J. Currie (STARLINK)
*     TIMJ: Tim Jenness (JAC, Hawaii)
*     {enter_new_authors_here}
 
*  History:
*     9-APR-1990 (RFWS):
*        Original version.
*     1991 February 26 (MJC):
*        Fixed bug when VEC routine returns bad status.
*     1996 May 20 (MJC):
*        Replaced LIB$ESTABLISH and LIB$REVERT calls.
*     2004 Oct 1 (TIMJ):
*        Recreate Generic version from constituent parts.
*        NUM_ERROR can no longer be accessed directly.
*     {enter_further_changes_here}
 
*  Bugs:
*     {note_any_bugs_here}
 
*-
 
*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing
 
*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT primitive data constants
 
*  Global Variables:
 
*  Arguments Given:
      LOGICAL BAD
      LOGICAL VAR
      INTEGER EL
      <TYPE> A( EL )
      <TYPE> VA( EL )
      <TYPE> B( EL )
      <TYPE> VB( EL )
 
*  Arguments Returned:
      <TYPE> C( EL )
      <TYPE> VC( EL )
      INTEGER NERR
 
*  Status:
      INTEGER STATUS             ! Global status
 
*  External References:
      EXTERNAL NUM_TRAP
      INTEGER NUM_TRAP           ! Numerical error handler
      EXTERNAL NUM_WASOK
      LOGICAL NUM_WASOK          ! Numeric operation successfull
      
 
*  Local Variables:
      INTEGER I                  ! Loop counter for array elements
      INTEGER IERR               ! Initial error position (dummy)
 
*.
 
*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN
 
*  No variance component to process
*  ================================

*  Simply multiply the two data arrays.
      IF ( .NOT. VAR ) THEN
         CALL ERR_MARK
         CALL VEC_MUL<T>( BAD, EL, A, B, C, IERR, NERR, STATUS )
 
*  Annul the bad status due to any overflows.
         CALL ERR_ANNUL( STATUS )
         CALL ERR_RLSE
 
*  Variance component present
*  ==========================

*  Establish a numerical error handler and initialise the numerical
*  error status and error count.
      ELSE
         CALL NUM_HANDL( NUM_TRAP )
         CALL NUM_CLEARERR()
         NERR = 0
 
*  No bad values present
*  =====================
         IF ( .NOT. BAD ) THEN
 
*  Multiply the data arrays, checking for numerical overflow after each
*  calculation.
            DO 1 I = 1, EL
               C( I ) = A( I ) * B( I )
               IF ( .NOT. NUM_WASOK() ) THEN
                  C( I ) = VAL__BAD<T>
                  NERR = NERR + 1
                  CALL NUM_CLEARERR()
               END IF
 
*  Derive the variance values, again checking for overflow.
               VC( I ) = VA( I ) * B( I ) * B( I ) +
     :                   VB( I ) * A( I ) * A( I )
               IF ( .NOT. NUM_WASOK() ) THEN
                  VC( I ) = VAL__BAD<T>
                  NERR = NERR + 1
                  CALL NUM_CLEARERR()
               END IF
    1       CONTINUE
 
*  Bad values present
*  ==================
         ELSE
            DO 2 I = 1, EL
 
*  See if either input data value is bad.  If so, then set bad output
*  values.
               IF ( ( A( I ) .EQ. VAL__BAD<T> ) .OR.
     :              ( B( I ) .EQ. VAL__BAD<T> ) ) THEN
                  C( I ) = VAL__BAD<T>
                  VC( I ) = VAL__BAD<T>
 
*  Multiply the data values, checking for numerical overflow.
               ELSE
                  C( I ) = A( I ) * B( I )
                  IF ( .NOT. NUM_WASOK() ) THEN
                     C( I ) = VAL__BAD<T>
                     NERR = NERR + 1
                     CALL NUM_CLEARERR()
                  END IF
 
*  See if either input variance value is bad.  If so, then set a bad
*  output variance value.
                  IF ( ( VA( I ) .EQ. VAL__BAD<T> ) .OR.
     :                 ( VB( I ) .EQ. VAL__BAD<T> ) ) THEN
                     VC( I ) = VAL__BAD<T>
 
*  Calculate the output variance value, again checking for overflow.
                  ELSE
                     VC( I ) = VA( I ) * B( I ) * B( I ) +
     :                         VB( I ) * A( I ) * A( I )
                     IF ( .NOT. NUM_WASOK() ) THEN
                        VC( I ) = VAL__BAD<T>
                        NERR = NERR + 1
                        CALL NUM_CLEARERR()
                     END IF
                  END IF
               END IF
    2       CONTINUE
         END IF
 
*  Remove the numerical error handler.
         CALL NUM_REVRT
      END IF
 
      END
