      SUBROUTINE KPG1_HSTQ<T>( BAD, EL, INARR, NUMBIN, OLDHST, MAP,
     :                         OUTARR, STATUS )
*+
*  Name:
*     KPG1_HSTQx

*  Purpose:
*     Equalises the histogram of an array.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL KPG1_HSTQx( BAD, EL, INARR, NUMBIN, OLDHST, MAP, OUTARR,
*                      STATUS )

*  Description:
*     This routine equalises (linearises) the histogram of an array.
*     Thus approximately equal numbers of different values appear in
*     output equalised version of the input array.

*  Arguments:
*     BAD = LOGICAL (Given)
*        If true there may be bad values within the input array.  If
*        false there are no bad values.  It is used as an efficiency
*        mechanism.
*     EL = INTEGER (Given)
*        The dimension of the arrays.
*     INARR( EL ) = ? (Given)
*        The array to be equalised.
*     NUMBIN = INTEGER (Given)
*        Number of bins used in histogram.  A moderately large number of
*        bins is recommended so that there is little artifactual
*        quantisation is introduced, say a few thousand except for
*        byte data.
*     OLDHST( NUMBIN ) = INTEGER (Returned)
*        The histogram of the input array, i.e. before equalisation.
*     MAP( NUMBIN ) = INTEGER (Returned)
*        Key to transform the input histogram's bin number to the
*        equalised bin number after histogram mapping.
*     OUTARR( EL ) = ? (Returned)
*        The array containing the equalised values.
*     STATUS = INTEGER (Given)
*        Global status value.

*  Notes:
*     -  There is a routine for each numeric data type: replace "x" in
*     the routine name by D, R, I, W, UW, B or UB as appropriate. The
*     arrays supplied to and returned from this routine must have the
*     data type specified.

*  Algorithm:
*     -  Get maximum and minimum values in array.  Generate the
*     histogram of the input array.  Initialise cumulative probability
*     variable
*     -  For all bins in the histogram: calculate current bin
*     probability and the cumulative probability; work out the
*     corresponding equalised histogram bin number; and set the key
*     element accordingly to record the mapped bin number.
*     -  Remap the input array according to the key.

*  References:
*     Gonzalez, R.C. and Wintz, P., 1977, "Digital Image Processing",
*     Addison-Wesley, pp. 118--126.

*  Copyright:
*     Copyright (C) 1991 Science & Engineering Research Council.
*     Copyright (C) 1995 Central Laboratory of the Research Councils.
*     Copyright (C) 2006 Particle Physics & Astronomy Research
*                   Council.
*     All Rights Reserved.

*  Licence:
*     This programme is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This programme is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE.  See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this programme; if not, write to the Free Software
*     Foundation, Inc., 51, Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     1991 November 8 (MJC):
*        Original version based on HSTQSB.
*     1995 February 21 (MJC):
*        Used modern subroutine prefixes, standardised comment
*        alignment, and sorted the variables.
*     2006 February 24 (MJC):
*        Added new CUMUL argument set to .FALSE. to KPG1_GHSTx call.
*     {enter_further_changes_here}

*  Bugs:
*     {note_new_bugs_here}

*-

*  Type Definitions:
      IMPLICIT  NONE             ! No default typing allowed

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Global SSE definitions

*  Arguments Given:
      LOGICAL BAD
      INTEGER EL
      <TYPE> INARR( EL )
      INTEGER NUMBIN

*  Arguments Returned:
      INTEGER OLDHST( NUMBIN )
      INTEGER MAP( NUMBIN )
      <TYPE> OUTARR( EL )

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      LOGICAL BAD2               ! There are bad values in the array
      REAL CUMPRB                ! Cumulative probability function
      INTEGER J                  ! Loop counter
      <LTYPE> MAXMUM             ! Maximum value in array
      INTEGER MAXPOS             ! Position of maximum found in array
      <LTYPE> MINMUM             ! Minimum value in array
      INTEGER MINPOS             ! Position of maximum found in array
      INTEGER NEWBIN             ! Bin counter for new histogram
      INTEGER NINVAL             ! Number of bad values in array
      REAL NUMPIX                ! Number of valid values in array
      REAL PROB                  ! Probability function for old bins

*.

*  Check the inherited status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Obtain the maximum and minimum values to define the bounds of the
*  histogram.  Derive the number of good pixels.
      CALL KPG1_MXMN<T>( BAD, EL, INARR, NINVAL, MAXMUM, MINMUM,
     :                   MAXPOS, MINPOS, STATUS )
      NUMPIX = REAL( EL - NINVAL )

*  The number of bad pixels has been counted so it might be possible to
*  save future processing.
      BAD2 = BAD .OR. ( NINVAL .EQ. 0 )

*  Generate the histogram between those bounds.
      CALL KPG1_GHST<T>( BAD2, EL, INARR, INARR, 0.0D0, NUMBIN, .FALSE.,
     :                   MAXMUM, MINMUM, OLDHST, STATUS )

*  Initialise the cumulative-probability variable.
      CUMPRB = 0.0

*  Loop round all the histogram bins in order to equalise the
*  histogram.
      DO  J = 1, NUMBIN

*  Calculate the probability for the jth bin, and the cumulative
*  probability so far.
         PROB = REAL( OLDHST( J ) ) / NUMPIX
         CUMPRB = CUMPRB + PROB

*  Work out the corresponding bin in the equalised histogram from the
*  cumulative probability and the number of bins.
         NEWBIN = NINT( CUMPRB * REAL( NUMBIN ) )
         NEWBIN = MIN( NUMBIN, MAX( 1, NEWBIN ) )

*  Keep track of which bin in the equalised histogram the pixels in the
*  jth bin of the old histogram belong.
         MAP( J ) = NEWBIN

*  End of the loop for all histogram bins to derive equalised
*  histogram.
      END DO

*  Apply the transformation via the key contained in the MAP array to
*  create an equalised output array.
      CALL KPG1_RMAP<T>( EL, INARR, MAXMUM, MINMUM, NUMBIN, MAP, OUTARR,
     :                   STATUS )

      END
