      SUBROUTINE CCG_WCWI<T>( NPIX, NLINES, COORDS, WIDTHS, STATUS )
*+
*  Name:
*     CCG_WCWIx

*  Purpose:
*     Creates a channel-width array from the channel co-ordinates.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL CCG_WCWIx( NPIX, NLINES, COORDS, WIDTHS, STATUS )

*  Description:
*     This routine accepts an array consisting of a series of
*     (vectorised) lines of co-ordinates.  For each pixel the 
*     subroutine forms a channel width by halving the difference
*     of neighbouring co-ordinates along a line of data, i.e.
*     it assumes that therre are no gaps.  The widths for first and
*     last pixels are the difference with its interior neighbour.

*  Arguments:
*     NPIX = INTEGER (Given)
*        The number of pixels in a line of data.
*     NLINES = INTEGER (Given)
*        The number of lines of data in the stack.
*     COORDS( NPIX, NLINES ) = ? (Given)
*        The world co-ordinates.
*     WIDTHS( NPIX, NLINES ) = ? (Given)
*        The chanel widths.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for each numeric data type: replace "x" in
*     the routine name by D or R as appropriate.  The COORDS and WIDTHS
*     arguments supplied to the routine must have the data type 
*     specified.

*  Authors:
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     2006 January 27 (MJC):
*        Original version.
*     2009 July 3 (MJC):
*         Renamed from CCG1_WCWIx.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants

*  Arguments Given:
      INTEGER NPIX
      INTEGER NLINES
      <TYPE> COORDS( NPIX, NLINES )

*  Arguments Returned:
      <TYPE> WIDTHS( NPIX, NLINES )

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      INTEGER I                  ! Loop variable
      INTEGER J                  ! Loop variable

*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Loop over all interior pixels.
      DO 2 J = 2, NLINES - 1
         DO 1 I = 1, NPIX

*  Both bracketing values need to be good.  Might review this
*  later to use central co-ordinate and the good neighbour.
            IF ( COORDS( I, J + 1 ) .EQ. VAL__BAD<T> .OR.
     :           COORDS( I, J - 1 ) .EQ. VAL__BAD<T> ) THEN
               WIDTHS( I, J ) = VAL__BAD<T>
            ELSE
               WIDTHS( I, J ) = ABS( COORDS( I, J + 1 ) - 
     :                               COORDS( I, J - 1 ) ) / 2<CONST>
            END IF
    1    CONTINUE
    2 CONTINUE

*  Now find the widths the first exterior element of each pixel.
      DO 3 I = 1, NPIX
         IF ( COORDS( I, 1 ) .EQ. VAL__BAD<T> .OR.
     :        COORDS( I, 2 ) .EQ. VAL__BAD<T> ) THEN
            WIDTHS( I, 1 ) = VAL__BAD<T>
         ELSE
            WIDTHS( I, 1 ) = ABS( COORDS( I, 2 ) - COORDS( I, 1 ) )
         END IF
    3 CONTINUE

      DO 4 I = 1, NPIX
         IF ( COORDS( I, NLINES ) .EQ. VAL__BAD<T> .OR.
     :        COORDS( I, NLINES - 1 ) .EQ. VAL__BAD<T> ) THEN
            WIDTHS( I, NLINES ) = VAL__BAD<T>
         ELSE
            WIDTHS( I, NLINES ) = ABS( COORDS( I, NLINES ) - 
     :                                 COORDS( I, NLINES - 1 ) )
         END IF
    4 CONTINUE

      END
