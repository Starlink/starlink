      SUBROUTINE CCG8_TMN3<T>( IGNORE, EL, ORDDAT, USED, TMEAN, STATUS )
*+
*  Name:
*     CCG8_TMN3x

*  Purpose:
*     Forms the n-trimmed mean of the given set of ordered data.
*     This variant does not process variances.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL CCG1_TMN3x( IGNORE, EL, ORDDAT, USED, TMEAN, STATUS )

*  Description:
*     The routine forms the trimmed mean of the given dataset.  The
*     IGNORE upper and lower ordered values are removed from
*     consideration.  Then the remaining values are added and averaged.
*     The values not rejected are indicated by setting the flags
*     in array used.

*  Arguments:
*     EL = INTEGER*8 (Given)
*        The number of entries in ORDDAT.
*     IGNORE = INTEGER*8 (Given)
*        The number of values to ignore from the upper and lower orders.
*     ORDDAT( EL ) = ? (Given)
*        The set of ordered data for which a trimmed mean is required.
*     USED( EL ) = LOGICAL (Returned)
*        Flags showing which values have not been rejected.
*     TMEAN = DOUBLE PRECISION (Returned)
*        The trimmed mean.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for real and double-precision: replace "x"
*     in the routine name by D or R as appropriate.  The ORDDAT
*     argument supplied to the routine must have the data type
*     specified.

*  Copyright:
*     Copyright (C) 1991 Science & Engineering Research Council.
*     Copyright (C) 2009 Science & Technology Facilities Council. All
*     Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     PDRAPER: Peter Draper (STARLINK)
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     28-MAR-1991 (PDRAPER):
*        Original version.
*     4-APR-1991 (PDRAPER):
*        Changed to remove n values from upper and lower orders
*        instead of a given fraction.
*     30-MAY-1991 (PDRAPER):
*        Added used array.
*     20-AUG-1991 (PDRAPER):
*        Variant of CCG1_TMN2 - variance analysis removed.
*     2009 July 3 (MJC):
*        Converted to generic and renamed from CCG1_TMN3R.  Moved EL
*        (after renaming from NSET) to the normal order in the API.
*     {enter_further_changes_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'PRM_PAR'          ! PRIMDAT constants

*  Arguments Given:
      INTEGER*8 IGNORE
      INTEGER*8 EL
      <TYPE> ORDDAT( EL )

*  Arguments Returned:
      LOGICAL USED( EL )
      DOUBLE PRECISION TMEAN

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      INTEGER*8 I                ! Loop variables
      INTEGER*8 LBND             ! Lower index of used values
      INTEGER*8 NLEFT            ! Number of values left to process
      INTEGER*8 UBND             ! Upper index of used values

*  Local references:
      INCLUDE 'NUM_DEC_CVT'
      INCLUDE 'NUM_DEF_CVT'      ! Primdat conversion definition
                                 ! functions
*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Make sure that there are enough variables left after ignoring the
*  outer ones. If there are not set output values bad and set used to
*  all false.
      NLEFT = EL - 2 * IGNORE
      IF ( NLEFT .LE. 0 ) THEN
         TMEAN = VAL__BADD
         DO 4 I = 1, EL
            USED( I ) = .FALSE.
    4    CONTINUE

      ELSE

*  Set the bounds.
         LBND = IGNORE + 1
         UBND = EL - IGNORE

*  Loop over all values forming the sum of values for trimmed mean.
*  Set used flags
         TMEAN = 0.0D0
         DO 1 I = 1, EL
            IF ( I .GE. LBND .AND. I .LE. UBND ) THEN
               TMEAN = NUM_<T>TOD( ORDDAT( I ) ) + TMEAN
               USED( I ) = .TRUE.
            ELSE
               USED( I ) = .FALSE.
            END IF
    1    CONTINUE
         TMEAN = TMEAN / DBLE( NLEFT )

      END IF

      END
