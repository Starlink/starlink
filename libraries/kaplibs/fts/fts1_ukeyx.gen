      SUBROUTINE FTS1_UKEY<T>( NCARD, BUFFER, STCARD, NAME, VALUE,
     :                         CMTBGN, COMNT, THERE, CARD, STATUS )
*+
*  Name:
*     FTS1_UKEYx

*  Purpose:
*     Writes the value to the specified keyword from a buffer of 
*     FITS-header card images.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL FTS1_UKEYx( NCARD, BUFFER, STCARD, NAME, VALUE, CMTBGN,
*                      COMNT, THERE, CARD, STATUS )

*  Description:
*     This routine searches a buffer containing the header card images
*     from a FITS file for card with keyword %NAME; and, if found,
*     override its original value with the given value %VALUE.  The
*     keyword can have a comment string leading by the character
*     specified by %CMTBGN.  The search ends when the next end of a
*     header block, marked by the END keyword, is encountered or the
*     the buffer is exhausted.  If the keyword is present %THERE is
*     true, otherwise it is false.  If the parameter is present more
*     than once in the header, only the first occurence will be used.
*
*     The name may be compound to permit writing of hierarchical
*     keywords.
*
*     The buffer of FITS header card image should be mapped in the
*     'UPDATE' mode.
*
*  Arguments:
*     NCARD = INTEGER (Given)
*        The number of card images in the buffer.
*     BUFFER( NCARD ) = CHARACTER * ( * ) (Given and Returned)
*        The buffer containing the header card images.
*     STCARD = INTEGER (Given)
*        The number of the card from which to start search.
*     NAME = CHARACTER *( * ) (Given)
*        The name of the keyword to be written a new value.  This may be
*        a compound name to handle hierarchical keywords, and it has the
*        form keyword1.keyword2.keyword3 etc.  The maximum number of
*        keywords per FITS card is 20.  Comparisons are performed in
*        uppercase and blanks are removed.  Each keyword must be no
*        longer than 8 characters.  The total length must not exceed
*        48 characters.  This is to allow for the value, and indentation
*        into a blank-keyword card (as hierarchical keywords are not
*        standard and so cannot be part of the standard keyword name).
*     VALUE = ? (Given)
*        The value to be used to override the original value of the
*        keyword.
*     CMTBGN = CHARACTER * ( 1 ) (Given)
*        The character which indicates the beginning of the comment
*        string of to be appended to the keyword.  Normally it is '/'.
*        when it is blank, no comment will be appended to the keyword.
*     COMNT = CHARACTER * ( * ) (Given)
*        The comment string of the keyword.  It may be truncated at the
*        end to put into the space left after writing keyword value.
*     THERE = LOGICAL (Returned)
*        If true, the specified keyword is present.
*     CARD = INTEGER (Returned)
*        The number of the last continuation comment card whose contents
*        are included in the returned string.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  There is a routine for each of the data types logical, integer,
*     64-bit integer, real, and double precision: replace "x" in the
*     routine name by L, I, K, R or D as appropriate.
*     -  The comments are written from column 32 or higher if the value
*     demands more than the customary 20 characters for the value.  A
*     comment may be omitted if the value is so long to leave no room.
*     -  The BUFFER appears out of standard order because when this
*     routine is called, BUFFER is expected to be a mapped array.
*     This order prevents having to append several %VAL(length)
*     arguments instead of just the one.

*  Authors:
*     WG: Wei Gong (IPMAF)
*     MJC: Malcolm J. Currie (STARLINK)
*     {enter_new_authors_here}

*  History:
*     1991 September 9 (WG):
*        Original version.
*     1994 July 16 (MJC):
*        Renamed and tidied.  Fixed violations of the Fortran standard
*        for appending to character-strings.  Fixed bugs that made
*        comments and values uppercase, filtered the comments, and
*        positioned the comment string in the wrong location.  Used
*        parameters to define lengths of the parts of a FITS header
*        card.  Does not attempt to write a blank comment.
*     1996 July 26 (MJC):
*        Allowed for values longer than 20 characters by shifting the
*        comment rightward.  Append the old comment, if requested,
*        after writing the value rather than trying to squeeze the
*        new value into the space left by the old.
*     1996 November 13 (MJC):
*        Renamed from FTS1_WKEYx to FTS1_UKEYx, and calls new
*        FTS1_WKEYx.
*     2012 May 10 (MJC):
*        Added 64-bit integer.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants

*  Arguments Given:
      INTEGER NCARD
      INTEGER STCARD
      CHARACTER * ( * ) NAME
      <TYPE> VALUE
      CHARACTER * ( 1 ) CMTBGN
      CHARACTER * ( * ) COMNT

*  Arguments Given and Returned:
      CHARACTER * ( 80 ) BUFFER( NCARD )

*  Arguments Returned:
      LOGICAL THERE
      INTEGER CARD

*  Status:
      INTEGER STATUS             ! Global status

*  External References:
      INTEGER CHR_LEN            ! The used length of a string

*  Local Constants:
      INTEGER COMCOL             ! Minimum column position for the start
                                 ! of a FITS comment
      PARAMETER ( COMCOL = 32 )

      INTEGER COMLN              ! Maximum number of characters in a
                                 ! FITS header card comment
      PARAMETER ( COMLN = 69 )

      INTEGER HKEYLN             ! Maximum number of characters in a
                                 ! FITS header card hierarchical keyword
      PARAMETER ( HKEYLN = 48 )

      INTEGER KEYLN              ! Maximum number of characters in a
                                 ! FITS header card keyword or
                                 ! hierarchical component thereof
      PARAMETER ( KEYLN = 8 )

      INTEGER MXWORD             ! Maximum number of hierarchical levels
                                 ! in a keyword
      PARAMETER ( MXWORD = 20 )

      INTEGER VALLN              ! Maximum number of characters in a
                                 ! FITS header card value
      PARAMETER ( VALLN = 70 )

*  Local Variables:
      INTEGER CDKYLN             ! Length of the keyword of a card
      CHARACTER * ( VALLN ) CHRVAL  ! The value in characters
      CHARACTER * ( HKEYLN ) CMPKEY ! Compound name
      INTEGER CMPLN              ! Length of compound name
      CHARACTER * ( COMLN ) CMTSTR ! Comment string of a keyword
      LOGICAL COMPND             ! Compound-keyword flag
      CHARACTER * ( KEYLN ) CRDKEY ! Keyword of a card image
      INTEGER ENDW( MXWORD )     ! End column of each keyword
      INTEGER EQUALS             ! The position of equal sign
      INTEGER I                  ! Do loop indicator
      INTEGER KEYLEN             ! Length of specified keyword
      CHARACTER * ( HKEYLN ) KEYWRD  ! Specified keyword
      INTEGER LSTAT              ! Local status
      INTEGER NWORD              ! Number of keywords in a card image
      LOGICAL OLDCOM             ! Use existing comment?
      INTEGER POSCOM             ! Position of the start of the
                                 ! existing comment (w.r.t. = sign)
      INTEGER POSDEL             ! Position of the start of the
                                 ! comment delimiter (w.r.t. = sign)
      INTEGER STARTW( MXWORD )   ! Start column of each keyword
      INTEGER WORDLN             ! Length of each word
      CHARACTER * ( VALLN ) WORDS( MXWORD ) ! Keywords and value in a
                                 ! card image

*.

*  Check the inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Remove blanks from the keyword to be searched.  Change the keyword to
*  uppercase and get its used length.
      KEYWRD = NAME
      CALL CHR_RMBLK( KEYWRD )
      CALL CHR_UCASE( KEYWRD )
      KEYLEN = CHR_LEN( KEYWRD )

*  Initialise the card number to be examined, and the found flag.
      CARD = MAX( 1, STCARD )
      THERE = .FALSE.

*  To see whether it is a compound name.
      COMPND = INDEX( KEYWRD, '.' ) .NE. 0

*  If the keyword to be searched for is not compound, ...
      IF ( .NOT. COMPND ) THEN

*  Compare the keyword on each card with the given keyword, until the
*  required card is found, or the 'END' card is met, or no card is left.
         DO WHILE ( ( .NOT. THERE ) .AND. ( CARD .LE. NCARD ) .AND.
     :              ( BUFFER( MIN( NCARD, CARD ) )( :3 ) .NE. 'END' ) )

*  Get the keyword of this card image, and its used length.
            CRDKEY = BUFFER( CARD )( :8 )
            CDKYLN = CHR_LEN( CRDKEY )

*  If the current card is the required keyword, ...
            IF ( CRDKEY( :CDKYLN ) .EQ. KEYWRD( :KEYLEN ) ) THEN

*  Set the found flag and get the position of the equals sign.
               THERE = .TRUE.
               EQUALS = INDEX( BUFFER( CARD ), '=' )

*  Otherwise go to the next card in the buffer.
            ELSE
               CARD = CARD + 1
            END IF
         END DO

*  If the keyword to be searched for is compound, ...
      ELSE

*  Get the keywords on the card and compare with given one, until the
*  required card is found, or the 'END' card is met, or no card is left.
         DO WHILE ( ( .NOT. THERE ) .AND. ( CARD .LE. NCARD ) .AND.
     :              ( BUFFER( MIN( NCARD, CARD ) )( :3 ) .NE. 'END' ) )

*  Keywords are located between the first character and the equals
*  sign.  So find the position of the equals sign first.
            EQUALS = INDEX( BUFFER( CARD ), '=' )

*  Then if the equals sign exists, extract the words from the FITS
*  card image.
            IF ( EQUALS .NE. 0 ) THEN
               CALL CHR_DCWRD( BUFFER( CARD )( :EQUALS - 1 ), MXWORD,
     :                         NWORD, STARTW, ENDW, WORDS, LSTAT )

*  Form the compound keyword by concatenating the words via the
*  delimiter.
               CMPLN = 0
               CMPKEY = ' '
               DO I = 1, NWORD
                  WORDLN = ENDW( I ) - STARTW( I ) + 1
                  CALL CHR_PUTC( WORDS( I )( :WORDLN ), CMPKEY, CMPLN )
                  IF( I .NE. NWORD ) CALL CHR_PUTC( '.', CMPKEY, CMPLN )
               END DO

*  Compare the (compound) keyword of the current card with that of the
*  compound keyword to be searched. If it is found, set the found flag.
               IF ( CMPKEY( :CMPLN ) .EQ. KEYWRD( :KEYLEN ) ) THEN
                  THERE = .TRUE.

*  Otherwise, go to the next card in the buffer.
               ELSE
                  CARD = CARD + 1
               END IF

*  If this card does not contain the equal sign, go to the next card.
            ELSE
               CARD = CARD + 1
            END IF
         END DO
      END IF

*  If the keyword is found, form the new image card.
      IF ( THERE ) THEN

*  Determine if the existing comment is to be used.  If it is, extract
*  the old comment.
         OLDCOM = CMTBGN .EQ. ' ' .OR. COMNT .EQ. ' '
         IF ( OLDCOM ) THEN

*  Select the first word following the equals sign.  Ignore the local
*  status as there will usually be a comment following, and we only
*  want the (non-complex) value.  Derive the number of characters
*  comprising the value.
            CALL CHR_DCWRD( BUFFER( CARD )( EQUALS + 1: ), 1, NWORD,
     :                      STARTW( 1 ), ENDW( 1 ), CHRVAL, LSTAT )

*  There are a number of special cases.  Deal with these in turn.
*  First allow for any trailing comment delimiter abutted to the value.
*  The column positions are with respect to the equals sign.
            POSDEL = INDEX( CHRVAL, '/' )
            POSCOM = POSDEL
            IF ( POSCOM .EQ. 0 ) POSCOM = ENDW( 1 ) + 1

*  Next look for the absence of a slash delimiter.
            POSDEL = INDEX( BUFFER( CARD )( POSCOM: ), '/' ) + POSCOM
            IF ( POSDEL .EQ. POSCOM ) THEN
               CMTSTR = BUFFER( CARD )( POSDEL + 1 + EQUALS: )

*  Discount a space following the slash.
            ELSE IF ( BUFFER( CARD )( POSDEL:POSDEL ) .EQ. ' ' )THEN
               CMTSTR = BUFFER( CARD )( POSDEL + 1 + EQUALS: )

*  The comment abuts the slash delimiter.
            ELSE
               CMTSTR = BUFFER( CARD )( POSDEL + EQUALS: )

            END IF

         ELSE
            CMTSTR = COMNT

         END IF

*  Call generic routine to write the header card.
         CALL FTS1_WKEY<T>( KEYWRD, VALUE, CMTBGN, CMTSTR,
     :                      BUFFER( CARD ), STATUS )

      END IF

 999  CONTINUE

      END
