      SUBROUTINE FTS1_GKEY<T>( NCARD, BUFFER, SCARD, NAME, NOCCUR,
     :                         THERE, VALUE, COMENT, CARD, STATUS )
*+
*  Name:
*     FTS1_GKEYx

*  Purpose:
*     Gets the value and comment of a named header from a buffer of
*     FITS-header card images.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL FTS1_GKEYx( NCARD, BUFFER, SCARD, NAME, NOCCUR, THERE,
*                      VALUE, COMENT, CARD, STATUS )

*  Description:
*     This routine searches a buffer containing the header card images
*     from a FITS file for a keyword NAME; and returns its value and
*     comment, and the number of the card image within the buffer array
*     that contains the named keyword.  The search ends when the next
*     end of a header block, marked by the END keyword, is encountered
*     or the buffer is exhausted.  If the keyword is present, THERE is
*     .TRUE., otherwise it is .FALSE.   Since all cards images are in
*     character format, type conversion is performed.  An error status
*     will be returned if the conversion has failed.  If the keyword
*     expected to be present more than once, then the argument NOCCUR
*     controls which occurrence will be retrieved.  If a keyword is not
*     found, then no error results and the argument VALUE remains
*     unmodified.
*
*     The name may be compound to permit reading of hierarchical
*     keywords.  This routine will probably only work for HISTORY,
*     COMMENT and ' ' (blank) if there is just one value given on the
*     line, i.e. only one "keyword = value" before any comment
*     marker.  An error will result otherwise.

*  Arguments:
*     NCARD = INTEGER (Given)
*        The number of card images in the buffer.
*     BUFFER( NCARD ) = CHARACTER * 80 (Given)
*        The buffer containing the header card images.
*     SCARD = INTEGER (Given)
*        The number of the card from where the search will begin.  This
*        is needed because the headers may contain a dummy header
*        prior to an extension.
*     NAME = CHARACTER * ( * ) (Given)
*        The name of the keyword whose value is required.  This may be
*        a compound name to handle hierarchical keywords, and it has
*        the form keyword1.keyword2.keyword3 etc.  The maximum number of
*        keywords per FITS card is 20.  Comparisons are performed in
*        uppercase and blanks are removed.  Each keyword must be no
*        longer than 8 characters.
*     NOCCUR = INTEGER (Given)
*        The value of this argument specifies which occurrence of a
*        keyword should be used, if multiple ones are expected.  Any
*        value less than or equal to 1 indicates the first occurrence.
*     THERE = LOGICAL (Returned)
*        If .TRUE., the keyword given by argument NAME is present,
*        regardless of the exit status.
*     VALUE = ? (Returned)
*        The value of the keyword.
*     COMENT = CHARACTER * ( * ) (Returned)
*        The comment associated with the keyword.
*     CARD = INTEGER (Returned)
*        The number of the card containing the named keyword.  If the
*        card could not be found this is set to zero.
*     STATUS = INTEGER (Given)
*        Global status value.

*  Notes:
*     - There is a routine for each of the data types logical, integer,
*     64-bit integer, real, and double precision: replace "x" in the
*     routine name by L, I, K, R or D as appropriate.
*     - The comments are written from column 32 or higher if the value
*     demands more than the customary 20 characters for the value.  A
*     comment may be omitted if the value is so long to leave no room.

*  Algorithm:
*     -  Initialise counter and flag.  Test whether the input name is
*     compound by looking for the delimiter.
*     -  If it is not compound loop for all cards until the last card,
*     or the requested card is located.  Compare each card with the
*     desired keyword.  Once the required occurrence of the card is
*     found, set returned flag to say keyword has been found, convert
*     from a character string to the value by extracting the next word
*     after the equals sign, otherwise go to the next card.
*     -  For a compound name loop for all cards until the last card,
*     or the requested card is located.  Go to the next card if the
*     current card does not contain an equals sign.  Generate the
*     compound keyword from the keywords before the equals sign.
*     Compare the compound keyword with the desired name.  Once the
*     required occurrence of the card is found set the returned flag to
*     say keyword has been found, find the extent of the value in the
*     card by finding any comment delimeter, convert from a character
*     string to the value, otherwise go to the next card.
*     -  Reset card number to zero if the keyword is not present

*  Implementation Deficiencies:
*     Cannot handle character data because the arguments to CHR_CTOC
*     are different from the other CHR_CTOx routines.  Also the
*     extraction of the value and allowing for quotes is quite
*     different.  The imaginary portion of complex data is not
*     processed.

*  Copyright:
*     Copyright (C) 1988-1991, 1994 Science & Engineering Research
*     Council.
*     Copyright (C) 1996 Central Laboratory of the Research Councils.
*     Copyright (C) 2012 Science & Technology Facilities Council.
*     All Rights Reserved.

*  Licence:
*     This programme is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either Version 2 of
*     the License, or (at your option) any later version.
*
*     This programme is distributed in the hope that it will be
*     useful, but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE.  See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this programme; if not, write to the Free Software
*     Foundation, Inc., 51, Franklin Street, Fifth Floor, Boston, MA
*     02110-1301, USA.

*  Authors:
*     MJC: Malcolm J. Currie (STARLINK)
*     PDRAPER: Peter Draper (STARLINK - Durham University)
*     {enter_new_authors_here}

*  History:
*     1988 September 22 (MJC):
*        Original version.
*     1989 July 28 (MJC):
*        Added extra argument CARD.
*     1990 November 19 (MJC):
*        Renamed from FTS1_GKEY, and converted the prologue to the SST
*        style.
*     1991 February 28 (MJC):
*        Converted BUFFER from an assumed-size to an adjustable array
*        via the NCARD argument in case the END keyword is missing.
*     1991 July 14 (MJC):
*        Modified to handle hierarchical keywords.
*     1991 August 31 (MJC):
*        Fixed bug in comparing card keyword with the selected keyword.
*     1996 July 25 (MJC):
*        Allow for values that extending beyond column 30.
*     1996 November 1 (MJC):
*        Reintegrated some of PDRAPER's changes made to create
*        IMG1_GKEYx, notably the ability to deal with keywords with
*        more than one occurrence, and "beefed up" error reports.
*     1996 November 14 (MJC):
*        Added COMENT argument.
*     2012 May 10 (MJC):
*        Added 64-bit integer.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT  NONE           ! no default typing allowed

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! SSE global definitions

*  Arguments Given:
      INTEGER NCARD              ! Number of FITS card images to search
      CHARACTER * ( 80 ) BUFFER( NCARD ) ! FITS tape buffer
      INTEGER SCARD              ! Search-start card number
      CHARACTER * ( * ) NAME     ! Name of the keyword
      INTEGER NOCCUR             ! Occurrence number

*  Arguments Returned:
      LOGICAL THERE              ! Is card containing the keyword
                                 ! present?
      <TYPE> VALUE               ! Value of the keyword
      CHARACTER * ( * ) COMENT   ! Comment associated with the keyword
      INTEGER CARD               ! Number of the card image containing
                                 ! keyword NAME

*  Status:
      INTEGER STATUS             ! Global status


*  External References:
      EXTERNAL CHR_LEN
      INTEGER CHR_LEN            ! Number of characters in a string
                                 ! ignoring trailing blanks

*  Local Constants:
      INTEGER KEYLN              ! Maximum number of characters in a
                                 ! FITS header card keyword or
                                 ! hierarchical component thereof
      PARAMETER ( KEYLN = 8 )

      INTEGER MXWORD             ! Maximum number of hierarchical levels
                                 ! in a keyword
      PARAMETER ( MXWORD = 20 )

*  Local Variables:
      CHARACTER * ( 60 ) CHRVAL  ! The value in characters
      CHARACTER * ( 72 ) CMPKEY  ! Compound name
      INTEGER COMCOL             ! Column number containing the comment
                                 ! character in the current card image
      LOGICAL COMPND             ! Supplied name is compound?
      CHARACTER * ( KEYLN ) CRDKEY ! Card keyword
      INTEGER ENDW( MXWORD )     ! End columns of each keyword in a card
                                 ! image
      INTEGER EQUALS             ! Column number containing the first
                                 ! equals sign in the current card image
      INTEGER I                  ! Loop counter
      INTEGER ISTAT              ! Local status
      CHARACTER * ( 80 ) KEYWRD  ! The compound keyword
      INTEGER NC                 ! Number of characters
      INTEGER NCHV               ! Number of characters in the value
      INTEGER NCK                ! Number of characters in the compound
                                 ! keyword
      INTEGER NF                 ! Number of occurrences found
      INTEGER NWORD              ! Number of keywords in the current
                                 ! card image
      INTEGER STARTW( MXWORD )   ! Start columns of each keyword in a
                                 ! card image
      CHARACTER * ( KEYLN ) WORDS( MXWORD ) ! The keywords in the
                                 ! current card image

*.

*  Check the inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Initialise some variables.
      CARD = MAX( 1, SCARD )
      THERE = .FALSE.
      NF = 0
      COMENT = ' '

*  Remove blanks from the keyword to be searched for, and make it
*  uppercase for comparisons.  Find its effective length.
      KEYWRD = NAME
      CALL CHR_UCASE( KEYWRD )
      CALL CHR_RMBLK( KEYWRD )
      NC = CHR_LEN( KEYWRD )

*  Is it a compound name?
      COMPND = INDEX( KEYWRD, '.' ) .NE. 0

*  The simple case.
*  ================
      IF ( .NOT. COMPND ) THEN

*  The maximum keyword length is defined by the FITS standard.
        NC = MIN( NC, KEYLN )

*  Now loop through the cards.  Compare the keyword on each word with
*  the given keyword, until the required card is found, or the 'END'
*  card is met, or there are no cards remaining.
   10    CONTINUE                ! Start of 'DO WHILE' loop
         IF ( ( .NOT. THERE ) .AND.
     :        ( CARD .LE. NCARD ) .AND.
     :        ( BUFFER( MIN( NCARD, CARD ) )( :3 ) .NE. 'END' ) ) THEN

*  Extract the keyword from this card image.
            CRDKEY = BUFFER( CARD )( :KEYLN )

*  Does the current card contain the required keyword?
            IF ( CRDKEY( :CHR_LEN( CRDKEY ) ) .EQ. KEYWRD( :NC ) ) THEN

*  The keyword is present.  Is this the occurrence we require?
               NF = NF + 1
               IF ( NF .GE. NOCCUR ) THEN

*  Yes it is.
                  THERE = .TRUE.

*  Select the first word following the equals sign.  Ignore the local
*  status as there will usually be a comment following, and we only
*  want the (non-complex) value.  Derive the number of characters
*  comprising the value.
                  CALL CHR_DCWRD( BUFFER( CARD )( 10: ), 1, NWORD,
     :                            STARTW( 1 ), ENDW( 1 ), CHRVAL,
     :                            ISTAT )
                  NCHV = ENDW( 1 ) - STARTW( 1 ) + 1

*  Remove any trailing comment delimiter.  This is in case there is a
*  comment abutted to the value.
                  COMCOL = INDEX( CHRVAL( :NCHV ), '/' )
                  IF ( COMCOL .NE. 0 ) NCHV = COMCOL - 1

*  Convert the string to the correct type.  Trap the occasion when the
*  value is a blank string (this may be true for COMMENT and HISTORY
*  records).
                  IF ( NCHV .GT. 0 ) THEN
                     CALL ERR_MARK
                     CALL CHR_CTO<T>( CHRVAL( :NCHV ), VALUE, STATUS )
                     IF ( STATUS .NE. SAI__OK ) THEN
                        CALL ERR_ANNUL( STATUS )
                        STATUS = SAI__ERROR
                     END IF
                     CALL ERR_RLSE
                  ELSE
                     STATUS = SAI__ERROR
                  END IF

*  Report the error context.
                  IF ( STATUS .EQ. SAI__ERROR ) THEN
                     CALL MSG_SETC( 'NAME', NAME( :NC ) )
                     CALL MSG_SETC( 'VALUE', CHRVAL( :NCHV ) )
                     CALL ERR_REP( 'FTS1_GKEY<T>_TYPCNV',
     :                    'Error converting FITS item ^NAME (with '/
     :                    /'value ^VALUE) to type REAL.', STATUS )
                  END IF
               ELSE

*  Move to the next card in the buffer.
                  CARD = CARD + 1
               END IF
            ELSE

*  Move to the next card in the buffer.
               CARD = CARD + 1
            END IF

*  Loop to the start of the 'DO WHILE'.
            GO TO 10
         END IF

*  Hierarchical-keyword case.
*  ==========================
      ELSE

*  Now loop through the cards ('END' terminates header).
   20    CONTINUE                ! Start of 'DO WHILE' loop
         IF ( ( .NOT. THERE ) .AND.
     :        ( CARD .LE. NCARD ) .AND.
     :        ( BUFFER( MIN( NCARD, CARD ) )( :3 ) .NE. 'END' ) ) THEN

*  Does the current card have a value, i.e. an equals sign?  This is
*  not foolproof because of the ING format et al. uses blank fields,
*  comments and history to store data.  Fortunately, these are not
*  likely to be compound names.
            EQUALS = INDEX( BUFFER( CARD ), '=' )
            IF ( EQUALS .NE. 0 ) THEN

*  Extract the words from the FITS card image up to the equals sign,
*  assuming these to be keywords.
               CALL CHR_DCWRD( BUFFER( CARD )( :EQUALS-1 ), MXWORD,
     :                         NWORD, STARTW, ENDW, WORDS, ISTAT )

*  Form the compound name if there is more than one supposed keyword by
*  concatenating the words via the delimiter.
               IF ( NWORD .GT. 1 ) THEN
                  NCK = 0
                  CMPKEY = ' '
                  DO I = 1, NWORD
                     NC = ENDW( I ) - STARTW( I ) + 1
                     CALL CHR_PUTC( WORDS( I )( :NC ), CMPKEY, NCK )
                     IF ( I .NE. NWORD )
     :                 CALL CHR_PUTC( '.', CMPKEY, NCK )
                  END DO

*  Merely copy the first keyword.
               ELSE
                  CMPKEY = WORDS( 1 )
                  NCK = ENDW( 1 ) - STARTW( 1 ) + 1
               END IF

*  Compare the (compound) keyword of the current card image with that
*  of the compound keyword being searched for in the buffer.
               IF ( CMPKEY( :NCK ) .EQ. KEYWRD( :NCK ) ) THEN

*  Have we located sufficient occurrences?
                  NF = NF + 1
                  IF ( NF .GE. NOCCUR ) THEN

*  The keyword is present.
                     THERE = .TRUE.

*  Find the upper range of columns that contains the value associated
*  with the hierarchical keyword.  This is achieved by looking for the
*  standard comment character.  If that's not present, treat the value
*  as the first word after the equals sign.  Although it is valid FITS,
*  this algorithm does not locate a comment that abuts a value without
*  a leading space or slash.  Hierarchical keywords are non-standard so
*  it pays to be flexible.  Allow for the offset of the equals sign
*  when finding the column that last contains part of the value.
                     COMCOL = INDEX( BUFFER( CARD )( EQUALS + 1: ),
     :                        '/' )
                     IF ( COMCOL .EQ. 0 ) THEN
                        CALL CHR_DCWRD( BUFFER( CARD )( EQUALS+1: ),
     :                                  MXWORD, NWORD, STARTW, ENDW,
     :                                  WORDS, ISTAT )
                        COMCOL = ENDW( 1 ) + EQUALS - 1
                     ELSE
                        COMCOL = COMCOL + EQUALS - 1
                     END IF

*  Extract the value as a character string.
                     CHRVAL = BUFFER( CARD )( EQUALS + 1:COMCOL )

*  Remove leading blanks and get number of characters comprising the
*  value.
                     CALL CHR_LDBLK( CHRVAL )
                     NCHV = CHR_LEN( CHRVAL )

*  Convert the string to the correct type.  Trap the occasion when the
*  value is a blank string (this may be true for COMMENT and HISTORY
*  records).
                     IF ( NCHV .GT. 0 ) THEN
                        CALL ERR_MARK
                        CALL CHR_CTO<T>( CHRVAL( :NCHV ), VALUE,
     :                                   STATUS )
                        IF ( STATUS .NE. SAI__OK ) THEN
                           CALL ERR_ANNUL( STATUS )
                           STATUS = SAI__ERROR
                        END IF
                        CALL ERR_RLSE
                     ELSE
                        STATUS = SAI__ERROR
                     END IF

*  Report the error context.
                     IF ( STATUS .EQ. SAI__ERROR ) THEN
                        CALL MSG_SETC( 'NAME', NAME( :NCK ) )
                        CALL MSG_SETC( 'VALUE', CHRVAL( :NCHV ) )
                        CALL ERR_REP( 'FTS1_GKEY<T>_TYPCNV',
     :                       'Error converting FITS item ^NAME ' //
     :                       '(with value ^VALUE) to type REAL.',
     :                       STATUS )
                     END IF
                  ELSE

*  Move to the next card in the buffer.
                     CARD = CARD + 1
                  END IF
               ELSE

*  Move to the next card in the buffer.
                  CARD = CARD + 1
               END IF
            ELSE

*  Move to the next card in the buffer.
               CARD = CARD + 1
            END IF

*  Move to the start of the 'DO WHILE' loop.
            GO TO 20
         END IF
      END IF

*  Extract the comment.  First try to find the recommended slash comment
*  delimiter.  If that's not present, extract the comment after the
*  space following the value.  When there is a slash either start
*  the comment immediately following the slash (when the comment abuts
*  the slash), or allow for the recommended space following the slash.
*  Other spaces are regarded as significant leading spaces in the
*  comment.
      IF ( THERE ) THEN
         COMCOL = INDEX( BUFFER( CARD )( NCHV + 1: ), '/' )
         IF ( COMCOL .EQ. 0 ) THEN
            COMENT = BUFFER( CARD )( NCHV + 2: )

         ELSE
            COMCOL = NCHV + COMCOL
            IF ( BUFFER( CARD )( COMCOL + 1:COMCOL + 1 ) .EQ. ' ' ) THEN
               COMENT = BUFFER( CARD )( COMCOL + 2: )
            ELSE
               COMENT = BUFFER( CARD )( COMCOL + 1: )
            END IF

         END IF

*  The card was not found.
      ELSE
         CARD = 0
      END IF

      END
